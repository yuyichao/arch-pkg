pkgname=julia-conda-git
pkgver=0.6.1.0.1.geca5315
pkgrel=1
pkgdesc="Conda.jl"
url="https://github.com/JuliaPy/Conda.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-pkg-scripts')
# TODO version depends
depends=('julia' 'julia-bindeps' 'julia-compat' 'julia-json')
provides=('julia-conda')
source=(git://github.com/JuliaPy/Conda.jl)
md5sums=('SKIP')

pkgver() {
  cd Conda.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Conda.jl

  # The build script captures the environment for the build user.
  cat > deps/deps.jl <<EOF
const ROOTENV = joinpath(homedir(), ".julia/conda/3")
const MINICONDA_VERSION = "3"
EOF
}

package() {
  cd Conda.jl

  . julia-install-pkg.sh Conda "${pkgdir}" "${pkgname}"
  mkdir -p "${dest_dir}/deps/usr"
}
