pkgname=julia-conda-git
pkgver=1.4.1
pkgrel=1
pkgdesc="Conda.jl"
url="https://github.com/JuliaPy/Conda.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-pkg-scripts')
depends=('julia' 'julia-json' 'julia-versionparsing')
provides=('julia-conda')
source=(git://github.com/JuliaPy/Conda.jl)
md5sums=('SKIP')

pkgver() {
  cd Conda.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Conda.jl

  # The build script captures the environment for the build user.
  cat > deps/deps.jl <<EOF
const ROOTENV = joinpath(dirname(@__FILE__), "usr")
const MINICONDA_VERSION = "3"
EOF
}

package() {
  cd Conda.jl

  . /usr/lib/julia/julia-install-pkg.sh Conda "${pkgdir}" "${pkgname}"
  mkdir -p "${dest_dir}/deps/usr"
}
