# $Id: PKGBUILD 108485 2014-03-27 14:48:16Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=pypy-pytools
pkgname=('pypy3-pytools' 'pypy-pytools')
pkgver=2014.2.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=(http://pypi.python.org/packages/source/p/pytools/pytools-${pkgver}.tar.gz)
sha512sums=('fd4532b521dbdbec0ff0d6fd838427f05cce84ac1f7a836fde36445f4ce0532369f6f1746d552afcf2c4e83b35158520c3910fa2a08ff82e14effc059ea6bbfb')

build() {
   cp -a pytools-${pkgver}{,-python2}

   cd "$srcdir/pytools-${pkgver}"
   pypy3 setup.py build

   cd "${srcdir}/pytools-${pkgver}-python2"
   pypy setup.py build
}

package_pypy-pytools() {
   depends=('pypy' 'pypy-decorator')
   cd "${srcdir}/pytools-${pkgver}-python2"
   pypy setup.py install --root="${pkgdir}" --skip-build --optimize=1
}

package_pypy3-pytools(){
   depends=('pypy3' 'pypy3-decorator')
   cd "${srcdir}/pytools-${pkgver}"
   pypy3 setup.py install --root="${pkgdir}" --skip-build --optimize=1 \
     --install-lib=/opt/pypy3/lib/python3.2/site-packages
}

