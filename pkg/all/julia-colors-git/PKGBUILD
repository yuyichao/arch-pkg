pkgname=julia-colors-git
pkgver=0.5.4.0.3.gf85cb64
pkgrel=1
pkgdesc="Colors.jl"
url="https://github.com/JuliaGraphics/Colors.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-colortypes' 'julia-fixedpointnumbers' 'julia-compat'
         'julia-reexport')
provides=('julia-colors')
source=(git://github.com/JuliaGraphics/Colors.jl)
md5sums=('SKIP')

pkgver() {
  cd Colors.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Colors.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Colors"

  cp -a src "${pkgdir}/${site_dir}/Colors"
}
