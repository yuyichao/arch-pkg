pkgname=julia-combinatorics-git
pkgver=0.3.1
pkgrel=1
pkgdesc="Combinatorics.jl"
url="https://github.com/JuliaLang/Combinatorics.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia' 'julia-polynomials' 'julia-iterators')
provides=('julia-combinatorics')
source=(git://github.com/JuliaLang/Combinatorics.jl)
md5sums=('SKIP')

pkgver() {
  cd Combinatorics.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Combinatorics.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Combinatorics"

  cp -a src "${pkgdir}/${site_dir}/Combinatorics"
}
