# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: xduugu

pkgbase=lib32-qtcurve
pkgname=lib32-qtcurve
true && pkgname=(lib32-qtcurve-utils lib32-qtcurve-qt4 lib32-qtcurve-gtk2)
pkgver=1.8.17
pkgrel=2
pkgdesc='A configurable set of widget styles for KDE and Gtk. Multilib.'
arch=('x86_64')
url="https://github.com/QtCurve/qtcurve"
license=('GPL')
groups=('qtcurve')
makedepends=('cmake' 'gcc-multilib' 'lib32-gtk2' 'lib32-qt4' 'lib32-libxcb')
source=($url/archive/$pkgver.tar.gz
  qconfig.h
  1e1b6408dea21c2bff532ddd87fe23bcde358556.patch)
md5sums=('c4f2e9bc296398b42bfc132b88a3b989'
         '89eaf62c5271cd6c268b926bb752a744'
         '200480e598c1bb317d9cc5dc68b22054')
options=('strip' 'debug')

prepare() {
  cd qtcurve-$pkgver

  patch -Np1 < ../1e1b6408dea21c2bff532ddd87fe23bcde358556.patch
}

build() {
  mkdir -p QtCore
  cp qconfig.h QtCore

  cd qtcurve-$pkgver

  # export multilib parameters
  # and QT_BUILD_KEY workaround
  export CC="gcc -m32 -I$srcdir -I$srcdir/QtCore"
  export CXX="g++ -m32 -I$srcdir -I$srcdir/QtCore"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  [ -d bld ] || mkdir bld && cd  bld

  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_PLUGINS_DIR=/usr/lib32/qt/plugins \
    -DQT_LIBRARY_DIR=/usr/lib32 \
    -DLIB_INSTALL_DIR=/usr/lib32 \
    -DQTC_QT4_ENABLE_KDE=false \
    -DENABLE_GTK2=true \
    -DENABLE_QT5=false

  make
}

package_lib32-qtcurve-qt4() {
  cd qtcurve-$pkgver/bld/qt4
  make DESTDIR="$pkgdir" install
}

package_lib32-qtcurve-gtk2() {
  cd qtcurve-$pkgver/bld/gtk2/style
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/share"
}

package_lib32-qtcurve-utils() {
  cd qtcurve-$pkgver/bld/qtcurve-utils
  make DESTDIR="$pkgdir" install
}
