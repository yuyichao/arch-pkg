# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: xduugu

pkgbase=lib32-qtcurve
pkgname=lib32-qtcurve
true && pkgname=(lib32-qtcurve-utils lib32-qtcurve-qt4 lib32-qtcurve-gtk2)
pkgver=1.8.18rc
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk. Multilib.'
arch=('x86_64')
url="https://github.com/QtCurve/qtcurve"
license=('LGPL')
groups=('qtcurve')
makedepends=('cmake' 'gcc-multilib' 'lib32-gtk2' 'lib32-qt4' 'lib32-libxcb')
source=($url/archive/$pkgver.tar.gz
  QtConfig
  qconfig.h)
md5sums=('4cfa01e5c32ec9a51d8ec38c8a708870'
         'a1f0a57f5ba462857773e0e6d73cfffd'
         '89eaf62c5271cd6c268b926bb752a744')
options=('strip' 'debug')

build() {
  mkdir -p QtCore
  cp qconfig.h QtCore
  cp QtConfig QtCore

  cd qtcurve-$pkgver

  # export multilib parameters
  # and QT_BUILD_KEY workaround
  export CC="gcc -m32 -I$srcdir -I$srcdir/QtCore"
  export CXX="g++ -m32 -I$srcdir -I$srcdir/QtCore"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  [ -d bld ] || mkdir bld && cd  bld

  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_PLUGINS_DIR=/usr/lib32/qt/plugins \
    -DQT_LIBRARY_DIR=/usr/lib32 \
    -DLIB_INSTALL_DIR=/usr/lib32 \
    -DQTC_QT4_ENABLE_KDE=false \
    -DENABLE_GTK2=true \
    -DENABLE_QT5=false

  make
}

package_lib32-qtcurve-qt4() {
  depends=(lib32-libxcb lib32-qtcurve-utils=${pkgver} lib32-qt4)

  make -C qtcurve-$pkgver/bld/qt4 DESTDIR="$pkgdir" install
}

package_lib32-qtcurve-gtk2() {
  depends=(lib32-libxcb lib32-qtcurve-utils=${pkgver} lib32-pango lib32-cairo
    lib32-gtk2 qtcurve-gtk2)
  make -C qtcurve-$pkgver/bld/gtk2/style DESTDIR="$pkgdir" install
  make -C qtcurve-$pkgver/bld/lib/cairo DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/share"
}

package_lib32-qtcurve-utils() {
  depends=(lib32-libx11 lib32-libxcb)

  make -C qtcurve-$pkgver/bld/lib/utils DESTDIR="$pkgdir" install
}
