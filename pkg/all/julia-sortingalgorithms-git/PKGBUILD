pkgname=julia-sortingalgorithms-git
pkgver=0.0.6
pkgrel=1
pkgdesc="SortingAlgorithms.jl"
url="https://github.com/JuliaLang/SortingAlgorithms.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia' 'julia-compat')
provides=('julia-sortingalgorithms')
source=(git://github.com/JuliaLang/SortingAlgorithms.jl)
md5sums=('SKIP')

pkgver() {
  cd SortingAlgorithms.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd SortingAlgorithms.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/SortingAlgorithms"

  cp -a src "${pkgdir}/${site_dir}/SortingAlgorithms"
}
