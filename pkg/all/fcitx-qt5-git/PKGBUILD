# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>

pkgbase=fcitx-qt5
pkgname=${pkgbase}-git
pkgver=20130301
pkgrel=1
pkgdesc="QT5 IM Module for Fcitx"
arch=('i686' 'x86_64')
url="http://code.google.com/p/fcitx"
license=('GPL')
depends=('fcitx-git' 'qt5-base')
makedepends=('git' 'cmake' 'intltool')
provides=('fcitx-qt5')
conflicts=('fcitx-qt5')
options=(!strip)

_gitname=fcitx-qt5

_gitroot=git://github.com/fcitx/fcitx-qt5/
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make install DESTDIR="${pkgdir}"
}
