# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: mickele
# Contributor: marcus fritzsch <fritschy@googlemail.com>

pkgbase=coin
pkgname=(coin coin-doc)
pkgver=3.1.3
pkgrel=14
pkgdesc='A high-level 3D graphics toolkit on top of OpenGL'
url='http://www.coin3d.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(doxygen openal fontconfig zlib freetype2 js simage bzip2 glu
             libgl expat libxext libsm libice libx11 mesa)
options=('!libtool' '!staticlibs' 'debug' 'strip')
source=("https://bitbucket.org/Coin3D/coin/downloads/Coin-${pkgver}.tar.gz")
sha256sums=('583478c581317862aa03a19f14c527c3888478a06284b9a46a0155fa5886d417')

build() {
  rm -rf pkg pkg-doc
  cd Coin-${pkgver}

  # fix prefix in coin-config
  sed -i '/^prefix/c prefix="/usr/"' bin/coin-config

  # fix compilation
  sed -i '/^#include "fonts\/freetype.h"$/i #include <cstdlib>\n#include <cmath>' src/fonts/freetype.cpp

  # fix http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=667139
  sed -i '/^#include <Inventor\/C\/basic.h>$/i #include <Inventor/C/errors/debugerror.h>' include/Inventor/SbBasic.h

  export CFLAGS="${CFLAGS} -g -O2"
  export CXXFLAGS="${CXXFLAGS} -g -O2"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-optimization \
      --enable-3ds-import \
      --enable-javascript-api \
      --enable-threadsafe \
      --enable-exceptions \
      --enable-man \
      --enable-html \
      --with-mesa \
      --enable-shared \
      --disable-maintainer-mode \
      --disable-dependency-tracking \
      --enable-system-expat

  make

  make install DESTDIR="${srcdir}/pkg"

  # final adjustments
  for _FILE in threads deprecated errors events ; do
    mv "${srcdir}/pkg/usr/share/man/man3/${_FILE}.3" \
       "${srcdir}/pkg/usr/share/man/man3/coin-${_FILE}.3"
  done

  mkdir -p "${srcdir}/pkg-doc/usr/share/"
  mv "${srcdir}/pkg/usr/share/doc" "${srcdir}/pkg-doc/usr/share/"
}

package_coin() {
  depends=(libgl expat libxext libsm libice libx11 mesa)
  optdepends=('simage: image format support'
              'openal: OpenAL Support'
              'fontconfig: Fontconfig support'
              'js: SpiderMonkey support'
              'freetype2: FreeType library'
              'zlib: zlib support'
              'bzip2: bzip2 support'
              'GLU: GLU support')
  cp -av pkg/* "${pkgdir}/"
}

package_coin-doc() {
  depends=(coin)
  cp -av pkg-doc/* "${pkgdir}/"
}
