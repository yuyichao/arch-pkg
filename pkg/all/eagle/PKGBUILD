pkgname=eagle
pkgver=6.5.0
pkgrel=1
pkgdesc="A powerful suite for schematic capture and printed circuit board design (aka eaglecad)"
arch=('i686' 'x86_64')
url="http://www.cadsoft.de/"
license=('custom')
if [[ $CARCH = i686 ]]; then
  depends=(
    'fontconfig'
    'freetype2'
    'gcc-libs'
    'libx11'
    'libxcursor'
    'libxext'
    'libxi'
    'libxrandr'
    'libxrender'
    'openssl'
    'zlib'
  )
else
  depends=(
    'lib32-fontconfig'
    'lib32-freetype2'
    'lib32-gcc-libs'
    'lib32-libx11'
    'lib32-libxcursor'
    'lib32-libxext'
    'lib32-libxi'
    'lib32-libxrandr'
    'lib32-libxrender'
    'lib32-openssl'
    'lib32-zlib'
  )
fi
# if you do not use Chinese and somehow don't have/want qt4 installed. Please
# comment out this makedepends as well as the "Fixing Chinese translation."
# section below.
makedepends=('qt4')
source=(ftp://ftp.cadsoft.de/$pkgname/program/${pkgver%.*}/$pkgname-lin-$pkgver.run
  $pkgname.desktop
  $pkgname.sh
  eagle_zh.patch)
md5sums=('721407ee4011d629c843c4c90d2a5082'
         '1c19a40d31404929446e885ae66451f9'
         'c6ab1922cd76bae647a20eadfc97ded2'
         '8664ab874ea32f5ab9853d2ded9c675f')
options=('!emptydirs')

package() {
  cd "$srcdir"

  msg2 "Extracting package content (this may take a while)..."
  install -dm755 "$pkgdir/opt"
  sed -e '1,/^__DATA__$/d' "$pkgname-lin-$pkgver.run" | \
    tar --no-same-owner -xjC "$pkgdir/opt"

  msg2 "Fixing path name and permission..."
  mv "$pkgdir/opt/$pkgname-$pkgver" "$pkgdir/opt/$pkgname"
  chmod -R a-s "$pkgdir/opt/$pkgname/"

  # Only the Chinese translation has this problem AFAIK.
  msg2 "Fixing Chinese translation."
  rm -rf "$srcdir/eagle_zh" &> /dev/null || true
  mkdir -p "$srcdir/eagle_zh"
  cd "$srcdir/eagle_zh"
  lconvert-qt4 -i "$pkgdir/opt/$pkgname/bin/eagle_zh.qm" \
    -o "$srcdir/eagle_zh/eagle_zh.ts"
  patch -Np0 < "$srcdir/eagle_zh.patch"
  lconvert-qt4 -o "$pkgdir/opt/$pkgname/bin/eagle_zh.qm" \
    -i "$srcdir/eagle_zh/eagle_zh.ts"
  cd "$srcdir"

  msg2 "Installing wrapper scripts..."
  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -dm755 "$pkgdir"/usr/share/{man/man1,licenses/$pkgname}
  ln -sf /opt/$pkgname/doc/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
  ln -sf /opt/$pkgname/doc/license_en.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
