pkgname=eagle
pkgver=6.6.0
pkgrel=1
pkgdesc="A powerful suite for schematic capture and printed circuit board design (aka eaglecad)"
arch=('i686' 'x86_64')
url="http://www.cadsoft.de/"
license=('custom')
if [[ $CARCH = i686 ]]; then
  depends=(
    'fontconfig'
    'freetype2'
    'gcc-libs'
    'libx11'
    'libxcursor'
    'libxext'
    'libxi'
    'libxrandr'
    'libxrender'
    'openssl'
    'zlib'
  )
else
  depends=(
    'lib32-fontconfig'
    'lib32-freetype2'
    'lib32-gcc-libs'
    'lib32-libx11'
    'lib32-libxcursor'
    'lib32-libxext'
    'lib32-libxi'
    'lib32-libxrandr'
    'lib32-libxrender'
    'lib32-openssl'
    'lib32-zlib'
  )
fi
source=(ftp://ftp.cadsoft.de/$pkgname/program/${pkgver%.*}/$pkgname-lin-$pkgver.run
  $pkgname.desktop
  $pkgname.sh)
md5sums=('a8cea964c409923f73e575f7ca866874'
         '1c19a40d31404929446e885ae66451f9'
         'c6ab1922cd76bae647a20eadfc97ded2')
options=('!emptydirs')

package() {
  cd "$srcdir"

  msg2 "Extracting package content (this may take a while)..."
  install -dm755 "$pkgdir/opt"
  sed -e '1,/^__DATA__$/d' "$pkgname-lin-$pkgver.run" | \
    tar --no-same-owner -xjC "$pkgdir/opt"

  msg2 "Fixing path name and permission..."
  mv "$pkgdir/opt/$pkgname-$pkgver" "$pkgdir/opt/$pkgname"
  chmod -R a-s "$pkgdir/opt/$pkgname/"

  cd "$srcdir"

  msg2 "Installing wrapper scripts..."
  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -dm755 "$pkgdir"/usr/share/{man/man1,licenses/$pkgname}
  ln -sf /opt/$pkgname/doc/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
  ln -sf /opt/$pkgname/doc/license_en.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
