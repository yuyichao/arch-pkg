# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2
pkgver=2.24.30+95+g876b5f7
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (legacy)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
depends=(atk pango libxcursor libxinerama libxrandr libxi libxcomposite libxdamage
         shared-mime-info cairo libcups gtk-update-icon-cache librsvg desktop-file-utils)
makedepends=(gobject-introspection python2 git gtk-doc)
optdepends=('gnome-themes-standard: Default widget theme'
            'adwaita-icon-theme: Default icon theme')
license=(LGPL)
install=gtk2.install
_commit=876b5f75faab5d8f3c60c3ec035b2ecd4ac5b67e
source=("git://git.gnome.org/gtk+#commit=$_commit"
        gtkrc
        gtk-query-immodules-2.0.hook
        xid-collision-debug.patch
        0001-do-not-apply-consumed_modifier-on-entry-modifiers.patch)
sha256sums=('SKIP'
            'bc968e3e4f57e818430130338e5f85a5025e21d7e31a3293b8f5a0e58362b805'
            '9656a1efc798da1ac2dae94e921ed0f72719bd52d4d0138f305b993f778f7758'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            'df92185aab973865a4b5389222e769aa21a2e13194e83873b272d4f57c785fcf')
options=('strip' 'debug' '!libtool')

pkgver() {
  cd gtk+
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk+
  patch -Np1 -i ../xid-collision-debug.patch
  patch -Np1 -i \
        ../0001-do-not-apply-consumed_modifier-on-entry-modifiers.patch
  sed -i '1s/python$/&2/' gtk/gtk-builder-convert
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtk+

  CXX=/bin/false ./configure --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --with-xinput=yes \
     --enable-gtk-doc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd gtk+
  make DESTDIR="$pkgdir" install

  install -Dm644 ../gtkrc "$pkgdir/usr/share/gtk-2.0/gtkrc"
  install -Dm644 ../gtk-query-immodules-2.0.hook "$pkgdir/usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook"

  rm "$pkgdir/usr/bin/gtk-update-icon-cache"
  rm -r "$pkgdir/usr/share/gtk-doc"
}
