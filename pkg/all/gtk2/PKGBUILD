# $Id: PKGBUILD 180158 2013-03-18 12:05:08Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gtk2
pkgname=('gtk2' 'gtk-update-icon-cache')
pkgver=2.24.20
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi'
  'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups'
  'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool' '!staticlibs' 'strip' 'debug')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
  xid-collision-debug.patch
  0001-do-not-apply-consumed_modifier-on-entry-modifiers.patch)
sha256sums=('cc66bcbf9239a7d9861175c681ba95894b55c70dc0b37aad8345c46ecfda0da3'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            'df92185aab973865a4b5389222e769aa21a2e13194e83873b272d4f57c785fcf')

build() {
  cd "${srcdir}/gtk+-$pkgver"
  patch -Np1 -i ../xid-collision-debug.patch
  patch -Np1 -i \
    ../0001-do-not-apply-consumed_modifier-on-entry-modifiers.patch

  CXX=/bin/false ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package_gtk2() {
  pkgdesc="GTK+ is a multi-platform toolkit (v2)"
  install=gtk2.install
  depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi'
    'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups'
    'gtk-update-icon-cache')
  backup=(etc/gtk-2.0/gtkrc)
  replaces=('gtk2-docs')

  cd "${srcdir}/gtk+-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
  echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/etc/gtk-2.0/gtkrc"
  #split this out to use with gtk3 too
  rm "$pkgdir/usr/bin/gtk-update-icon-cache"
}
package_gtk-update-icon-cache() {
  pkgdesc="The GTK+ update icon cache tool"
  depends=('gdk-pixbuf2>=2.24.1-3')

  cd "${srcdir}/gtk+-$pkgver/gtk"

  install -D -m755 gtk-update-icon-cache \
    "$pkgdir/usr/bin/gtk-update-icon-cache"
}
