pkgname=julia-compilersupportlibraries_jll-git
pkgver=0.3.3+0.0.1.g6e75bc7
pkgrel=1
pkgdesc="CompilerSupportLibraries_jll.jl"
url="https://github.com/JuliaBinaryWrappers/CompilerSupportLibraries_jll.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia gcc-libs)
provides=('julia-compilersupportlibraries_jll')
source=(git://github.com/JuliaBinaryWrappers/CompilerSupportLibraries_jll.jl)
md5sums=('SKIP')

pkgver() {
  cd CompilerSupportLibraries_jll.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd CompilerSupportLibraries_jll.jl

  /usr/lib/julia/julia-gen-jll.sh \
    CompilerSupportLibraries_jll \
    libgcc_s=libgcc_s.so.1 libgfortran libgomp libstdcxx=libstdc++
}

package() {
  cd CompilerSupportLibraries_jll.jl

  . /usr/lib/julia/julia-install-pkg.sh CompilerSupportLibraries_jll "${pkgdir}" "${pkgname}"
}
