_pkgname=pocl
pkgname=$_pkgname-git
pkgver=0.9.0.RC3.486.gb2c53ac
pkgrel=1
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=(i686 x86_64)
url="https://launchpad.net/pocl"
license=('GPL')
depends=('clang' 'libcl' 'hwloc' 'libltdl' 'opencl-headers')
options=(debug strip !staticlibs !libtool)
# pocl use pkg-config to find OpenCL, which is only provided in ocl-icd
makedepends=('ocl-icd')
provides=('pocl')
conflicts=('pocl')

_gitname=pocl

_gitroot=https://github.com/pocl/pocl
_gitref=master
exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref" --
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  git submodule init
  git submodule sync
  git submodule update
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/${_gitname}"

  #
  # BUILD HERE
  #
  ./autogen.sh
  ./configure --enable-shared --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/usr/include/CL"
}
