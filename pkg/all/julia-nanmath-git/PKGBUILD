pkgname=julia-nanmath-git
pkgver=0.1.0.0.3.gbb67c53
pkgrel=1
pkgdesc="NaNMath.jl"
url="https://github.com/mlubin/NaNMath.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-nanmath')
source=(git://github.com/mlubin/NaNMath.jl)
md5sums=('SKIP')

pkgver() {
  cd NaNMath.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd NaNMath.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/NaNMath"

  cp -a src "${pkgdir}/${site_dir}/NaNMath"
}
