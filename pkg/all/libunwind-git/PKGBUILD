# Maintainer: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - aarch64 support patch
#  - build v5 with system libatomic_ops

pkgname=libunwind-git
pkgver=1.1.63.gc56fb8f
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('i686' 'x86_64' 'aarch64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('glibc' 'xz')
source=('git://git.savannah.nongnu.org/libunwind')
md5sums=('SKIP')
options=('debug' 'strip')
provides=(libunwind)
conflicts=(libunwind)

pkgver() {
  cd libunwind

  git describe --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd libunwind

  autoreconf -fi
  [[ $CARCH == "arm" ]] && LDFLAGS+=" -latomic_ops" && CFLAGS+=" -DAO_USE_PTHREAD_DEFS=1"

  ./configure CFLAGS="$CFLAGS -U_FORTIFY_SOURCE" --prefix=/usr
  make
}

check() {
  cd libunwind

  # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
  make check || return 0
}

package() {
  cd libunwind

  make DESTDIR="$pkgdir/" install
}
