pkgname=julia-compat-git
pkgver=0.7.17
pkgrel=1
pkgdesc="Compat.jl"
url="https://github.com/JuliaLang/Compat.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia')
provides=('julia-compat')
source=(git://github.com/JuliaLang/Compat.jl)
md5sums=('SKIP')

pkgver() {
  cd Compat.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Compat.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/Compat/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/Compat/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
