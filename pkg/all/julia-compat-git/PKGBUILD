pkgname=julia-compat-git
pkgver=0.7.6.0.10.gab43b41
pkgrel=1
pkgdesc="Compat.jl"
url="https://github.com/JuliaLang/Compat.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia')
provides=('julia-compat')
source=(git://github.com/JuliaLang/Compat.jl)
md5sums=('SKIP')

pkgver() {
  cd Compat.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Compat.jl

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Compat"

  cp -a src "${pkgdir}/${site_dir}/Compat"
}
