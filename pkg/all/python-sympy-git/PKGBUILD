#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgbase=python-sympy
pkgname=${pkgbase}-git
_realver=0.7.2
pkgver=20130207
pkgrel=1
pkgdesc="A Python library for symbolic mathematics. Python3 git version"
depends=('python' 'python-numpy')
optdepends=('ipython: user friendly interface for isympy')
arch=('i686' 'x86_64')
makedepends=('git')
url="https://github.com/sympy/sympy"
license=('BSD')
provides=("${pkgbase}=${_realver}")
conflicts=("${pkgbase}")

_gitname=sympy

_gitroot=git://github.com/sympy/sympy.git
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  bin/use2to3
  cd py3k-sympy
  python setup.py build
}

package() {
  cd "$srcdir/$_gitname/py3k-sympy"

  python setup.py install --prefix=/usr --root "${pkgdir}" --optimize=1

  mv "${pkgdir}/usr/bin/isympy" "${pkgdir}/usr/bin/isympy-py3"
  mv "${pkgdir}/usr/share/man/man1/isympy.1" \
    "${pkgdir}/usr/share/man/man1/isympy-py3.1"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}
