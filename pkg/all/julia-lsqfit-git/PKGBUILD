pkgname=julia-lsqfit-git
pkgver=0.0.1.0.6.g42fcfe7
pkgrel=1
pkgdesc="LsqFit.jl"
url="https://github.com/JuliaOpt/LsqFit.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-calculus' 'julia-distributions' 'julia-optim')
provides=('julia-lsqfit')
source=(git://github.com/JuliaOpt/LsqFit.jl)
md5sums=('SKIP')

pkgver() {
  cd LsqFit.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd LsqFit.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/LsqFit"

  cp -a src "${pkgdir}/${site_dir}/LsqFit"
}
