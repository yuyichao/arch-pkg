# Maintainer: Igor Babuschkin <ibabuschkin at gmail dot com>

pkgname=python-meta-git
pkgver=0.1.2.0.38.g6136057
epoch=1
pkgrel=1
_realver=0.1.2
pkgdesc="Python Meta Programming"
url="https://github.com/numba/Meta"
arch=('i686' 'x86_64')
license=('BSD')
depends=('python>=3.4' 'python<3.5')
makedepends=(git python-setuptools)
provides=("python-meta=${_realver}")
conflicts=('python-meta')

_gitname=python-meta

_gitroot=https://github.com/numba/Meta.git
_gitref=master

exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/.0./' -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  python setup.py build
}

package() {
  cd "$srcdir/$_gitname"

  python setup.py install --prefix=/usr --root="$pkgdir"
}
