# Maintainer: broke <wutbroke@gmail.com>
# Based on Muflone's <webreg@vbsimple.net> Spyder PKGBUILD

pkgname=python-spyder
_pkgname=spyder
pkgver=2.3.0beta4
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='https://bitbucket.org/spyder-ide/spyderlib'
license=('MIT')
install="${pkgname}.install"
depends=('python-pyqt4' 'python-pyflakes' 'desktop-file-utils' 'gtk-update-icon-cache' 'python-pyzmq' 'python-pygments')
makedepends=('python-sphinx' 'python-setuptools')
optdepends=('python-pylint: powerful code analysis'
            'ipython: enhanced Python interpreter'
            'python-rope: editor code completion, calltips and go-to-definition'
            'python-sphinx: object inspector''s rich text mode'
            'python-numpy: N-dimensional arrays'
            'python-sympy: Symbolic manipulation package'
            'python-scipy: signal/image processing'
            'python-psutil: memory/CPU usage in the status bar'
            'python-h5py: HDF5 support'
            'python-matplotlib: 2D/3D plotting'
            'pep8-python3: python style guide checker')
source=("https://bitbucket.org/spyder-ide/spyderlib/downloads/${_pkgname}-${pkgver}.zip")
md5sums=('1ce4bad314fc8e2dcada30e1e8527f27')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${_pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
