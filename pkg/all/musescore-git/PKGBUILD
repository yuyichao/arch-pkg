# Contributor: Dr.Egg <hondoheal@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=musescore
pkgname=${pkgbase}-git
_realver=2.0b
pkgver=20130223
pkgrel=1
pkgdesc="A music score editor - git-version"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('shared-mime-info' 'alsa-lib' 'zlib' 'libvorbis' 'qt' 'qtwebkit'
  'qtscriptgenerator' 'portaudio' 'libpulse' 'libsndfile' 'libogg' 'gcc-libs')
makedepends=('cmake' 'doxygen' 'git')
optdepends=('jack')
conflicts=("${pkgbase}" 'mscore' 'musescore-svn')
provides=("${pkgbase}=${_realver}" 'musescore-svn')
replaces=('musescore-svn')
source=('mime.xml')
md5sums=('969696178e56de36f9af37d7da61baaa')
install=musescore.install

_gitname="MuseScore"

_gitroot="https://github.com/musescore/MuseScore.git"
_gitref=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  make PREFIX=/usr release
}

package() {
  cd "${srcdir}/${_gitname}/build.release"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/mime.xml" \
    "$pkgdir/usr/share/mime/packages/x-musescore.xml"
  ln -s mscore "$pkgdir/usr/bin/musescore"
}
