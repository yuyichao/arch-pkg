# Contributor: Dr.Egg <hondoheal@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=musescore
pkgname=${pkgbase}-git
_realver=2.0.0
pkgver=3745.1b4d81d
pkgrel=1
epoch=1
pkgdesc="A music score editor - git-version"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('shared-mime-info' 'alsa-lib' 'zlib' 'libvorbis'
  'qt5-base' 'qt5-webkit' 'qt5-tools' 'qt5-quick1' 'qt5-script'
  'qt5-xmlpatterns' 'qt5-svg' 'qt5-declarative' 'qt5-sensors'
  'portaudio' 'libpulse' 'libsndfile' 'libogg' 'gcc-libs')
makedepends=('cmake' 'doxygen' 'git')
optdepends=('jack')
conflicts=("${pkgbase}" 'mscore' 'musescore-svn')
provides=("${pkgbase}=${_realver}" 'musescore-svn')
replaces=('musescore-svn')
source=('mime.xml'
  '0001-fix-for-pch.patch'
  '0002-fix-for-manual-install.patch'
  '0001-add-back-native-style.patch'
)
md5sums=('969696178e56de36f9af37d7da61baaa'
         '8d866e9f1be43b290cf4a786bfd6c3e6'
         '012b58d5bde58912f3b4e071115e8843'
         '89873833d232901ed0db9a6fd591e5a2')
install=musescore.install
options=(strip debug)

_gitname="MuseScore"

_gitroot="https://github.com/musescore/MuseScore.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  # FIXME: Remove hack for gcc 4.8 when the gcc bug is fixed. gcc 4.8 seems to
  # crash when using pre-compiled headers, remove the following patch to
  # (probably) gain some compiling speed if you are not affected by the issue.
  patch -Np1 < ../0001-fix-for-pch.patch
  patch -Np1 < ../0002-fix-for-manual-install.patch

  # Upstream add hard coded builtin oxygen based theme (which has X11 support
  # remove, therefore no menu shadow under KDE) and they refuse to add it back.
  # This patch add (back) an option to use system theme.
  # More patch may be coming adding icon theme support later if necessary.
  patch -Np1 < ../0001-add-back-native-style.patch
  export CXXFLAGS="-std=gnu++0x -Wno-unused-local-typedefs -Wno-unused-parameter -Wno-maybe-uninitialized -Wno-unused-function ${CXXFLAGS}"
  mkdir -p build.release
  cd build.release
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=On
  make lrelease -j1
  make
}

package() {
  cd "${srcdir}/${_gitname}/build.release"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/mime.xml" \
    "$pkgdir/usr/share/mime/packages/x-musescore.xml"
  ln -s mscore "$pkgdir/usr/bin/musescore"
  install -dm755 "$pkgdir/usr/share/mscore-2.0/manual"
  mv manual/plugins_manual/plugins/* \
    "$pkgdir/usr/share/mscore-2.0/manual/plugins"
}
