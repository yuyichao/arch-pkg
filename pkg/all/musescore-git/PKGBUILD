# Contributor: Dr.Egg <hondoheal@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=musescore
pkgname=${pkgbase}-git
_realver=2.0b
pkgver=1940.aa8f987
pkgrel=1
epoch=1
pkgdesc="A music score editor - git-version"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('shared-mime-info' 'alsa-lib' 'zlib' 'libvorbis' 'qt4' 'qtwebkit'
  'qtscriptgenerator' 'portaudio' 'libpulse' 'libsndfile' 'libogg' 'gcc-libs')
makedepends=('cmake' 'doxygen' 'git')
optdepends=('jack')
conflicts=("${pkgbase}" 'mscore' 'musescore-svn')
provides=("${pkgbase}=${_realver}" 'musescore-svn')
replaces=('musescore-svn')
source=('mime.xml'
  '0001-fix-for-pch.patch'
  '0002-fix-for-manual-install.patch'
)
md5sums=('969696178e56de36f9af37d7da61baaa'
         'f743e73e5a7dc10eac61b1e5d1c3f717'
         '012b58d5bde58912f3b4e071115e8843')
install=musescore.install
options=(strip debug)

_gitname="MuseScore"

_gitroot="https://github.com/musescore/MuseScore.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  # FIXME: Remove hack for gcc 4.8 when the gcc bug is fixed. gcc 4.8 seems to
  # crash when using pre-compiled headers, remove the following patch to
  # (probably) gain some compiling speed if you are not affected by the issue.
  patch -Np1 < ../0001-fix-for-pch.patch
  patch -Np1 < ../0002-fix-for-manual-install.patch
  export CXXFLAGS="-std=gnu++0x -Wno-unused-local-typedefs -Wno-unused-parameter -Wno-maybe-uninitialized -Wno-unused-function ${CXXFLAGS}"
  mkdir -p build.release
  cd build.release
  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_SKIP_RPATH=On
  make lrelease -j1
  make
}

package() {
  cd "${srcdir}/${_gitname}/build.release"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/mime.xml" \
    "$pkgdir/usr/share/mime/packages/x-musescore.xml"
  ln -s mscore "$pkgdir/usr/bin/musescore"
  install -dm755 "$pkgdir/usr/share/mscore-2.0/manual"
  mv manual/plugins_manual/plugins/* \
    "$pkgdir/usr/share/mscore-2.0/manual/plugins"
}
