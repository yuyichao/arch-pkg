pkgname=julia-kerneldensity-git
pkgver=0.1.2
pkgrel=1
pkgdesc="KernelDensity.jl"
url="https://github.com/JuliaStats/KernelDensity.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-statsbase' 'julia-distributions' 'julia-optim'
         'julia-grid' 'julia-compat')
provides=('julia-kerneldensity')
source=(git://github.com/JuliaStats/KernelDensity.jl)
md5sums=('SKIP')

pkgver() {
  cd KernelDensity.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd KernelDensity.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/KernelDensity"

  cp -a src "${pkgdir}/${site_dir}/KernelDensity"
}
