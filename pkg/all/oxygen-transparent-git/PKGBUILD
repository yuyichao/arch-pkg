# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgbase=oxygen-transparent
pkgname=oxygen-transparent-git
pkgver=20130209
pkgrel=1
pkgdesc="Oxygen style and decoration with support for transparency"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/artwork/oxygen-transparent"
license=('GPL' 'LGPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'git' 'automoc4')
provides=(${pkgbase})
conflicts=(${pkgbase})
options=('!strip')

_gitname="oxygen-transparent"

_gitroot="git://anongit.kde.org/oxygen-transparent"
_gitref=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  mkdir -p build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}/build"

  make DESTDIR="${pkgdir}" install
}
