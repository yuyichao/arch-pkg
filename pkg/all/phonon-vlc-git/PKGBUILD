# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>

pkgbase=phonon-vlc
pkgname=phonon-vlc-git
pkgver=20130227
pkgrel=1
pkgdesc="Phonon VLC backend, git version"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdesupport/phonon/phonon-vlc"
license=('LGPL')
depends=('phonon-git' 'vlc')
makedepends=('git' 'automoc4' 'cmake' 'phonon')
provides=('phonon-vlc' 'phonon-backend')
conflicts=('phonon-vlc')
options=('!strip')

_gitname="phonon-vlc"

_gitroot="git://anongit.kde.org/phonon-vlc"
_gitref=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make "DESTDIR=$pkgdir" install
}
