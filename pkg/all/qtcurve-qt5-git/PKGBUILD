# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=qtcurve-qt5
pkgname=${pkgbase}-git
_realver=1.8.18
pkgver=1.8.18rc.6.g3812d42
pkgrel=1
epoch=2
pkgdesc='A configurable set of widget styles for KDE and Gtk. Qt5 experimental port version. Not finished, with known bugs. Patch/help welcome.'
arch=('i686' 'x86_64')
url="http://craigd.wikispaces.com/"
license=('GPL')
groups=('qtcurve')
depends=('qt5-base' 'qt5-svg' 'libxcb' 'qt5-x11extras' 'libx11')
makedepends=('cmake' 'git')
provides=("${pkgbase}=${_realver}" "qtcurve-utils=${_realver}")
conflicts=("${pkgbase}" "qtcurve-utils")
options=('debug' 'strip')

_gitname="qtcurve"

_gitroot="git://github.com/QtCurve/qtcurve.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build


  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQTC_KDE4_PREFIX=/usr \
    -DENABLE_QT4=Off \
    -DENABLE_QT5=On \
    -DENABLE_GTK2=Off
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make DESTDIR="$pkgdir/" -C qt5 install
  make DESTDIR="$pkgdir/" -C lib/utils install
}
