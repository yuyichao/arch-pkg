pkgname=julia-cartesian-git
pkgver=0.2.2.0.2.g0b21672
pkgrel=1
pkgdesc="Cartesian.jl"
url="https://github.com/timholy/Cartesian.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-cartesian')
source=(git://github.com/timholy/Cartesian.jl)
md5sums=('SKIP')

pkgver() {
  cd Cartesian.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Cartesian.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.txt "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Cartesian"

  cp -a src "${pkgdir}/${site_dir}/Cartesian"
}
