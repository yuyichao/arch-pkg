pkgname=julia-gadfly-git
pkgver=0.3.18.0.21.gfcb2496
pkgrel=1
pkgdesc="Gadfly.jl"
url="https://github.com/dcjones/Gadfly.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-codecs' 'julia-colors' 'julia-compat' 'julia-compose'
         'julia-contour' 'julia-dataframes' 'julia-datastructures'
         'julia-distributions' 'julia-hexagons' 'julia-iterators'
         'julia-json' 'julia-kerneldensity' 'julia-loess' 'julia-showoff'
         'julia-statsbase')
provides=('julia-gadfly')
source=(git://github.com/dcjones/Gadfly.jl)
md5sums=('SKIP')

pkgver() {
  cd Gadfly.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Gadfly.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Gadfly"

  cp -a src "${pkgdir}/${site_dir}/Gadfly"
}
