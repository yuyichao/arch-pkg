pkgname=julia-ipopt-git
pkgver=0.2.0
pkgrel=1
pkgdesc="Ipopt.jl"
url="https://github.com/JuliaOpt/Ipopt.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-mathprogbase' 'coin-or-ipopt')
provides=('julia-ipopt')
source=(git://github.com/JuliaOpt/Ipopt.jl)
md5sums=('SKIP')

pkgver() {
  cd Ipopt.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Ipopt.jl/deps

  julia -f ./build.jl
}

package() {
  cd Ipopt.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Ipopt"

  cp -a src deps "${pkgdir}/${site_dir}/Ipopt"

  rm "${pkgdir}/${site_dir}/Ipopt/deps/build.jl"
}
