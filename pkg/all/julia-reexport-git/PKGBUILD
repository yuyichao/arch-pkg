pkgname=julia-reexport-git
pkgver=0.0.3.0.2.g80a065e
pkgrel=1
pkgdesc="Reexport.jl"
url="https://github.com/simonster/Reexport.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-reexport')
source=(git://github.com/simonster/Reexport.jl)
md5sums=('SKIP')

pkgver() {
  cd Reexport.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Reexport.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Reexport"

  cp -a src "${pkgdir}/${site_dir}/Reexport"
}
