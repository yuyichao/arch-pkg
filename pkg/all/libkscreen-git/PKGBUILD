pkgname=libkscreen-git
pkgver=20130330
pkgrel=1
pkgdesc="KDE's screen management software."
url="http://quickgit.kde.org/?p=libkscreen.git"
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs')
makedepends=('git' 'cmake' 'automoc4' 'qjson')
provides=('libkscreen')
conflicts=('libkscreen')

_gitname="libkscreen"

_gitroot="git://anongit.kde.org/libkscreen.git"
_gitref=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make DESTDIR="$pkgdir" install
}
