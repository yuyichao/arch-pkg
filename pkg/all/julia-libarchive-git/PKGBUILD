pkgname=julia-libarchive-git
pkgver=0.0.2.0.1.g74ebbe8
pkgrel=1
pkgdesc="LibArchive.jl"
url="https://github.com/yuyichao/LibArchive.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps' 'julia-pkg-scripts')
# TODO version depends
depends=('julia' 'julia-compat' 'libarchive')
provides=('julia-libarchive')
source=(git://github.com/yuyichao/LibArchive.jl)
md5sums=('SKIP')

pkgver() {
  cd LibArchive.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd LibArchive.jl/deps

  julia --startup-file=no ./build.jl
}

package() {
  cd LibArchive.jl

  . julia-install-pkg.sh LibArchive "${pkgdir}" "${pkgname}"
}
