pkgname=julia-libarchive-git
pkgver=0.0.1.0.14.gf2e0102
pkgrel=1
pkgdesc="LibArchive.jl"
url="https://github.com/yuyichao/LibArchive.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'libarchive')
provides=('julia-libarchive')
source=(git://github.com/yuyichao/LibArchive.jl)
md5sums=('SKIP')

pkgver() {
  cd LibArchive.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd LibArchive.jl/deps

  julia -f ./build.jl
}

package() {
  cd LibArchive.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/LibArchive"

  cp -a src deps "${pkgdir}/${site_dir}/LibArchive"

  rm "${pkgdir}/${site_dir}/LibArchive/deps/build.jl"
}
