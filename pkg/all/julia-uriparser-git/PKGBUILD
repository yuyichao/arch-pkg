pkgname=julia-uriparser-git
pkgver=0.1.0.0.9.g54a8dd1
pkgrel=1
pkgdesc="URIParser.jl"
url="https://github.com/JuliaLang/URIParser.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia')
provides=('julia-uriparser')
source=(git://github.com/JuliaWeb/URIParser.jl)
md5sums=('SKIP')

pkgver() {
  cd URIParser.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd URIParser.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/URIParser"

  cp -a src "${pkgdir}/${site_dir}/URIParser"
}
