pkgname=julia-uriparser-git
pkgver=0.1.3.0.6.gcee3ce5
pkgrel=1
pkgdesc="URIParser.jl"
url="https://github.com/JuliaLang/URIParser.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia')
provides=('julia-uriparser')
source=(git://github.com/JuliaWeb/URIParser.jl)
md5sums=('SKIP')

pkgver() {
  cd URIParser.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd URIParser.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/URIParser/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/URIParser/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
