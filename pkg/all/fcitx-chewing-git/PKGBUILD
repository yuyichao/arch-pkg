# Contributor: taylorchu <taylorchu@gmail.com>

pkgbase=fcitx-chewing
pkgname=${pkgbase}-git
pkgver=20130205
pkgrel=1
pkgdesc="Fcitx Wrapper for libchewing"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-chewing"
license=('GPLv2')
depends=('fcitx' 'fcitx>=4.2.7' 'libchewing')
makedepends=('git' 'cmake' 'gettext' 'coreutils' 'sh')
provides=("${pkgbase}")
conflicts=("${pkgbase}")

_gitname=fcitx-chewing

_gitroot=https://github.com/fcitx/fcitx-chewing
_gitbranch=master

build(){
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Creating make environment..."
  mkdir -p build
  cd build

  msg "Starting make..."
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make DESTDIR="$pkgdir" install
}
