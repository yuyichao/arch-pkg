pkgname=julia-pdmats-git
pkgver=0.3.3.0.8.gd1e7445
pkgrel=1
pkgdesc="PDMats.jl"
url="https://github.com/JuliaStats/PDMats.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-arrayviews')
provides=('julia-pdmats')
source=(git://github.com/JuliaStats/PDMats.jl)
md5sums=('SKIP')

pkgver() {
  cd PDMats.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd PDMats.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/PDMats"

  cp -a src "${pkgdir}/${site_dir}/PDMats"
}
