#

_pkgname=jupyter-nbextension-rise
pkgname="${_pkgname}-git"
pkgver=5.5.0.32.g80e58d0
pkgrel=1
pkgdesc='RISE: "Live" Reveal.js Jupyter/IPython Slideshow Extension'
url="https://github.com/damianavila/RISE"
arch=('any')
license=('BSD')
depends=('jupyter' 'python>=3.8' 'python<3.9')
makedepends=('git')
provides=("${_pkgname}" jupyter-rise)
conflicts=("${_pkgname}" jupyter-rise)
source=(git://github.com/damianavila/RISE)
md5sums=('SKIP')
install=jupyter-rise.install

pkgver() {
  cd RISE

  git describe | sed -e 's/-/./g'
}

prepare() {
  rm -rf home
  mkdir -p home/.jupyter/nbconfig
}

build() {
  cd RISE
  LANG=en_US.utf8 python setup.py build
}

package() {
  cd RISE
  local _python3="$(readlink /usr/bin/python3)"
  local _site_packages="$pkgdir/usr/lib/$_python3/site-packages"
  mkdir -p "${_site_packages}"
  LANG=en_US.utf8 PYTHONPATH="$_site_packages:$PYTHONPATH" \
      python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  LANG=en_US.utf8 PYTHONPATH="$_site_packages:$PYTHONPATH" \
      JUPYTER_PATH="$pkgdir/usr/" \
      jupyter-nbextension install rise --py --system \
      --prefix="${pkgdir}/usr"
  mv "$pkgdir/"{usr/etc,etc}
}
