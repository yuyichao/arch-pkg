pkgname=julia-imagemagick-git
pkgver=0.0.0.0.90.9b93798
pkgrel=1
pkgdesc="ImageMagick.jl"
url="https://github.com/JuliaIO/ImageMagick.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-fixedpointnumbers' 'julia-colortypes' 'julia-fileio'
         'julia-compat' 'julia-images' 'imagemagick')
provides=('julia-imagemagick')
source=(git://github.com/JuliaIO/ImageMagick.jl)
md5sums=('SKIP')

pkgver() {
  cd ImageMagick.jl

  echo "0.0.0.0.$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd ImageMagick.jl

  julia -f deps/build.jl
}

package() {
  cd ImageMagick.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/ImageMagick"

  cp -a src deps "${pkgdir}/${site_dir}/ImageMagick"

  rm -rf "${pkgdir}/${site_dir}/ImageMagick/deps/"build.jl
}
