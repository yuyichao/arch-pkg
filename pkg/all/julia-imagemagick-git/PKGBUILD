pkgname=julia-imagemagick-git
pkgver=0.2.3
pkgrel=1
pkgdesc="ImageMagick.jl"
url="https://github.com/JuliaIO/ImageMagick.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps' 'julia-pkg-scripts')
# TODO version depends
depends=(julia julia-compat julia-fixedpointnumbers julia-colortypes julia-imagecore julia-fileio
         imagemagick)
provides=(julia-imagemagick)
source=(git://github.com/JuliaIO/ImageMagick.jl)
md5sums=('SKIP')

pkgver() {
  cd ImageMagick.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd ImageMagick.jl

  julia --startup-file=no deps/build.jl
}

package() {
  cd ImageMagick.jl

  . julia-install-pkg.sh ImageMagick "${pkgdir}" "${pkgname}"
}
