pkgname=julia-zmq-git
pkgver=0.3.1.0.6.g721ca92
pkgrel=1
pkgdesc="ZMQ.jl"
url="https://github.com/JuliaLang/ZMQ.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat' 'zeromq')
provides=('julia-zmq')
options=('debug' 'strip')
source=(git://github.com/JuliaLang/ZMQ.jl)
md5sums=('SKIP')

pkgver() {
  cd ZMQ.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd ZMQ.jl

  julia -f deps/build.jl
}

package() {
  cd ZMQ.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/ZMQ/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/ZMQ/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done

  rm -rf "${dest_dir}/deps/"build.jl
}
