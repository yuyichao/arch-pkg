pkgname=julia-zmq-git
pkgver=0.1.17.0.6.gb401232
pkgrel=1
pkgdesc="ZMQ.jl"
url="https://github.com/JuliaLang/ZMQ.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat' 'zeromq')
provides=('julia-zmq')
options=('debug' 'strip')
source=(git://github.com/JuliaLang/ZMQ.jl)
md5sums=('SKIP')

pkgver() {
  cd ZMQ.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd ZMQ.jl

  julia -f deps/build.jl
}

package() {
  cd ZMQ.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/ZMQ"

  cp -a src deps "${pkgdir}/${site_dir}/ZMQ"

  rm "${pkgdir}/${site_dir}/ZMQ/deps/build.jl"
}
