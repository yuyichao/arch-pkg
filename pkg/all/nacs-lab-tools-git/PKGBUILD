#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgname=nacs-lab-tools-git
pkgver=32.ea94999
pkgrel=2
epoch=1
pkgdesc="Linux Namespace Tools"
arch=('i686' 'x86_64')
provides=('nacs-lab-tools')
conflicts=('nacs-lab-tools')
url="https://github.com/nacs-lab/tools"
license=('GPLv3')
depends=(ncurses5-compat-libs)
optdepends=()
makedepends=('git' 'cmake')
options=('debug' 'strip')

_gitname='nacs-lab-tools'

_gitroot='git://github.com/nacs-lab/tools.git'
_gitref=master
exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build
  export JAVA_HOME=/opt/java6
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib \
    -DJAVA_HOME="${JAVA_HOME}"
  make
}

package() {
  cd "${srcdir}/$_gitname/build"

  make install DESTDIR="${pkgdir}"
}
