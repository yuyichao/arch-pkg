pkgname=julia-zeromq_jll-git
pkgver=4.3.2+2.0.1.gd7873c8
pkgrel=3
pkgdesc="ZeroMQ_jll.jl"
url="https://github.com/JuliaBinaryWrappers/ZeroMQ_jll.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia zeromq)
provides=('julia-zeromq_jll')
source=(git://github.com/JuliaBinaryWrappers/ZeroMQ_jll.jl)
md5sums=('SKIP')

pkgver() {
  cd ZeroMQ_jll.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd ZeroMQ_jll.jl

  libzmq=$(/usr/lib/julia/julia-read-soname.sh /usr/lib/libzmq.so)

  cat > src/ZeroMQ_jll.jl <<EOF
module ZeroMQ_jll
export libzmq
const PATH_list = String[]
const LIBPATH_list = String[]
const libzmq = "$libzmq"
end
EOF
  rm -rf src/wrappers/
  rm Artifacts.toml
}

package() {
  cd ZeroMQ_jll.jl

  . /usr/lib/julia/julia-install-pkg.sh ZeroMQ_jll "${pkgdir}" "${pkgname}"
}
