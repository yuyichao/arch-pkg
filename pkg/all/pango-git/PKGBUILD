# Contributor: mariusz - myswiat <my.swiat@o2.pl>

pkgbase=pango
pkgname=${pkgbase}-git
_realver=1.34.0
pkgver=1.34.0.1.g4542138
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
url="http://gnome.org/"
license=('LGPL')
depends=('glib2-git' 'cairo-git' 'libxft' 'libthai' 'freetype2'
  'harfbuzz-git' 'fontconfig-git')
makedepends=('gobject-introspection' 'libxt' 'gtk-doc')
provides=("${pkgbase}=${_realver}")
conflicts=("${pkgbase}")
install=${pkgbase}.install
options=('!libtool' '!emptydirs' 'strip' 'debug')

_gitname="pango"

_gitroot="http://git.gnome.org/browse/pango"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --with-included-modules=basic-fc
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR=$pkgdir install
  install -m755 -d "${pkgdir}/etc/pango"
}
