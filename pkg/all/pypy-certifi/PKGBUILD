pkgbase=pypy-certifi
pkgname=('pypy3-certifi' 'pypy-certifi')
_libname=${pkgbase/pypy-/}
pkgver=14.05.14
pkgrel=1
pkgdesc="Mozilla's SSL Certs"
arch=(any)
url="http://pypi.python.org/pypi/certifi"
license=('GPL')
makedepends=('pypy3-setuptools' 'pypy-setuptools')
source=("http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz")
sha512sums=('5edba0cc432c27212e31085fb3c2f2ded6fa177d716116111517c25b980d9fc206a9a6d47e2c001762e951ff50203b269d0eb245a039df20188b1136f3b700b2')

prepare() {
  cp -a $_libname-$pkgver{,-py2}

  cd "$srcdir/$_libname-$pkgver-py2"
  sed -i '1s|python$|pypy|' certifi/core.py

  cd "$srcdir/$_libname-$pkgver"
  sed -i '1s|python$|pypy3|' certifi/core.py
}

build() {
  cd "$srcdir/$_libname-$pkgver"
  pypy3 setup.py build

  cd "$srcdir/$_libname-$pkgver-py2"
  pypy setup.py build
}

package_pypy3-certifi() {
  depends=('pypy3')

  cd "$srcdir/$_libname-$pkgver"
  pypy3 setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-certifi() {
  depends=('pypy')

  cd "$srcdir/$_libname-$pkgver-py2"
  pypy setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
