# Maintainer: polterge|st

pkgbase=atk
pkgname=${pkgbase}-git
_realver=2.7.2
pkgver=20130205
pkgrel=1
pkgdesc="git version of atk"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('LGPL')
depends=('glib2')
makedepends=('pkgconfig' 'gobject-introspection')
options=('!libtool')
provides=("${pkgbase}=${_realver}")
conflicts=("${pkgbase}")

_gitname="atk"

_gitroot="git://git.gnome.org/atk"
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  # autoreconf --force --install

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
