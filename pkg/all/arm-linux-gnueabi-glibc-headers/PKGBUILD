# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: Erico Nunes <nunes dot erico at gmail>

_target="arm-linux-gnueabi"
pkgname=${_target}-glibc-headers
pkgver=2.21
pkgrel=1
pkgdesc="Embedded GNU C Library. Stage 1 - headers only. Needed for initialy build toolchain (${_target})"
arch=(any)
url="http://www.eglibc.org"
license=('GPL' 'LGPL')
depends=("${_target}-linux-api-headers>=3.16")
provides=("${_target}-eglibc-headers=${pkgver}")
replaces=("${_target}-eglibc-headers")
conflicts=("${_target}-eglibc-headers")
makedepends=("${_target}-gcc-stage1" 'subversion')
options=(!strip)
source=(http://ftp.gnu.org/gnu/libc/glibc-${pkgver}.tar.xz
        glibc-2.21-roundup.patch)
md5sums=('9cb398828e8f84f57d1f7d5588cf40cd'
         'bf9d96b11c76b113606aae102da63d9d')

prepare() {
  cd glibc-${pkgver}

  # glibc-2.21..75adf430
  patch -p1 -i ../glibc-2.21-roundup.patch

  rm -rf "${srcdir}"/glibc-build
  mkdir -p "${srcdir}"/glibc-build
}

build() {
  cd glibc-${pkgver}

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  cd "${srcdir}"/glibc-build

  export CFLAGS="-U_FORTIFY_SOURCE  -mlittle-endian -msoft-float -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../glibc-${pkgver}/configure \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --prefix=/ \
    --with-headers=/usr/${_target}/include \
    --enable-add-ons=libidn,ports,nptl \
    --enable-add-ons \
    --disable-profile \
    --without-gd \
    --without-cvs

  # make some libs and stubs
  make csu/subdir_lib
}

package() {
  cd "$srcdir/glibc-build"

  make install-headers "install_root=${pkgdir}/usr/${_target}" \
    install-bootstrap-headers=yes

  mkdir -p "${pkgdir}/usr/${_target}/lib"
  cp csu/crt1.o csu/crti.o csu/crtn.o "${pkgdir}/usr/${_target}/lib"

  # create stub lib
  ${_target}-gcc -nostdlib -nostartfiles -shared -x c /dev/null \
    -o "${pkgdir}/usr/${_target}/lib/libc.so"

  mkdir -p "${pkgdir}/usr/${_target}/usr"
  ln -s ../{include,lib} "${pkgdir}/usr/${_target}/usr"
}
