# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: Weng XueTian <wengxt AT gmail com>
# Contributor: lh <jarryson AT gmail com>

_pkgname=fcitx
pkgname=lib32-$_pkgname-git
pkgver=4.2.7.135.gf067641
_realver=4.2.8
pkgrel=1
epoch=1
pkgdesc="A Flexible Input Method Framework"
arch=("x86_64")
license=('GPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=("fcitx-git" "${_pkgname}>=${_realver}")
makedepends=('git' 'gettext' 'cmake' 'coreutils' 'sh' 'lib32-qt4' 'lib32-gtk2'
  # 'lib32-gtk3'
)
conflicts=('lib32-fcitx' 'lib32-fcitx-qt4' 'lib32-fcitx-qt'
  'lib32-fcitx-gtk2' 'lib32-fcitx-gtk3')
provides=("lib32-fcitx=${_realver}" "lib32-fcitx-qt=${_realver}"
  "lib32-fcitx-qt4=${_realver}" "lib32-fcitx-gtk2=${_realver}"
  # "lib32-fcitx-gtk3=${_realver}"
)
optdepends=(
  'lib32-qt4: for qt input method module'
  'lib32-gtk2: for gtk2 input method module'
  # 'lib32-gtk3: for gtk3 input method module'
)
install="$_pkgname.install"
source=(qconfig.h)
md5sums=('89eaf62c5271cd6c268b926bb752a744')
options=('strip' 'debug')

_gitname="fcitx"

_gitroot="git://github.com/fcitx/fcitx.git"
_gitref="master"

# _gitroot="git://github.com/yuyichao/fcitx.git"
# _gitref="codegen"

# _gitroot="ssh://git@bitbucket.org/yuyichao/fcitx.git"
# _gitref="wayland"

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)

  cd "$srcdir"

  mkdir -p QtCore
  cp qconfig.h QtCore

  msg "Creating make environment..."
  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  export CC="gcc -m32 -I$srcdir -I$srcdir/QtCore"
  export CXX="g++ -m32 -I$srcdir -I$srcdir/QtCore"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DENABLE_CAIRO=Off \
    -DENABLE_LIBXML2=Off \
    -DENABLE_TABLE=Off \
    -DENABLE_ICU=Off \
    -DLIB_INSTALL_DIR=/usr/lib32 \
    -DENABLE_GIR=Off \
    -DENABLE_QT_IM_MODULE=On \
    -DENABLE_GTK2_IM_MODULE=On \
    # -DENABLE_GTK3_IM_MODULE=On

  make -C src/lib
  make -C src/frontend/qt
  make -C src/frontend/gtk2
}

package() {
  cd "$srcdir/$_gitname-build"
  make -C src/lib DESTDIR="$pkgdir" install
  make -C src/frontend/gtk2 DESTDIR="$pkgdir" install
  install -Dm755 src/frontend/qt/qtim-fcitx.so \
    "$pkgdir/usr/lib32/qt/plugins/inputmethods/qtim-fcitx.so"
  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/lib32/pkgconfig"
}
