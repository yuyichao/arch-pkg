pkgname=julia-graphs-git
pkgver=0.5.4.0.19.g0853610
pkgrel=1
pkgdesc="Graphs.jl"
url="https://github.com/JuliaLang/Graphs.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-datastructures' 'julia-compat')
provides=('julia-graphs')
source=(git://github.com/JuliaLang/Graphs.jl)
md5sums=('SKIP')

pkgver() {
  cd Graphs.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Graphs.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Graphs"

  cp -a src "${pkgdir}/${site_dir}/Graphs"
}
