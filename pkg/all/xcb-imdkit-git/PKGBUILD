# Maintainer: Yichao Yu <yyc1992 AT gmail.com>

pkgname=xcb-imdkit-git
pkgver=55.2c68697
pkgrel=1
pkgdesc="input method development support for xcb"
arch=("i686" "x86_64")
license=('LGPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=(libxcb xcb-util libxau libxdmcp xcb-util-keysyms)
makedepends=(git cmake extra-cmake-modules)
options=('strip' 'debug')
source=(git://github.com/wengxt/xcb-imdkit)
md5sums=('SKIP')


pkgver() {
  cd xcb-imdkit

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd xcb-imdkit

  rm -rf build
  mkdir build
  cd build

  [[ -f CMakeCache.txt ]] && rm -v CMakeCache.txt
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd xcb-imdkit/build

  make DESTDIR="$pkgdir" install
}
