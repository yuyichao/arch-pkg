pkgname=julia-clustering-git
pkgver=0.3.3.0.25.g3f1fd76
pkgrel=1
pkgdesc="Clustering.jl"
url="https://github.com/JuliaStats/Clustering.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-arrayviews' 'julia-distances' 'julia-statsbase'
         'julia-compat')
provides=('julia-clustering')
source=(git://github.com/JuliaStats/Clustering.jl)
md5sums=('SKIP')

pkgver() {
  cd Clustering.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Clustering.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Clustering"

  cp -a src "${pkgdir}/${site_dir}/Clustering"
}
