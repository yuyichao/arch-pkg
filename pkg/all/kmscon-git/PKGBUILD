# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=kmscon-git
pkgver=7.42.g4e0b981
pkgrel=1
epoch=1
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=(i686 x86_64)
url='https://github.com/dvdhrm/kmscon'
license=('MIT')
depends=(systemd libdrm mesa libegl libgbm libgles pango dbus
  libxkbcommon xkeyboard-config wayland-git)
makedepends=('git' 'libxslt' 'docbook-xsl' 'linux-api-headers')
provides=(kmscon)
conflicts=(kmscon)
options=(!libtool)

_gitname=kmscon

_gitroot=https://github.com/dvdhrm/kmscon.git
_gitref=master

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  local ver=$(git describe | sed -e 's/-/./g')
  [[ $ver =~ ^[^0-9]*[.]*(.*) ]] && ver=${BASH_REMATCH[1]}
  echo "$ver"
}

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --enable-wlterm --with-renderers=cairo,bbulk,gltex
  make
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname" \
    "$pkgdir/usr/lib/systemd/system"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/"
  cp docs/kmscon{,vt@}.service "$pkgdir/usr/lib/systemd/system/"
}
