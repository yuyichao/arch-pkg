USE_PYPY=1

pkgname=pypy3-hg
pkgrel=1
pkgver=r72146.aca5ce9b7a02
_realver=2.3
pkgdesc="A Python3 implementation written in Python, JIT enabled, hg version"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('libffi')
provides=("pypy3=${_realver}")
conflicts=('pypy3')
options=(!buildflags)
makedepends=('python' 'mercurial' 'python2' 'tk')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module'
            'tk: tk module')
license=('custom:MIT')
source=("hg+https://bitbucket.org/pypy/pypy#branch=py3k")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}"/pypy
  echo "r$(hg id -n).$(hg id -i)"
}

build() {
  cd "${srcdir}"/pypy/pypy/goal

  export CFLAGS="-O1"
  export CXXFLAGS="-O1"

  _PYTHON=python2
  ((USE_PYPY)) && which pypy &> /dev/null && _PYTHON=pypy

  $_PYTHON ../../rpython/bin/rpython -Ojit --shared --output=pypy3-c \
    --gcrootfinder=shadowstack targetpypystandalone --objspace-std-withtproxy
}

package() {
  cd "${srcdir}"
  mkdir -p licenses/{libbz2-,openssl}
  ln -sf /usr/share/licenses/bzip2/LICENSE licenses/libbz2-/copyright
  ln -sf /usr/share/licenses/openssl/LICENSE licenses/openssl/copyright

  cd "${srcdir}"/pypy/pypy/tool/release

  _PYTHON=python2
  ((USE_PYPY)) && which pypy 2> /dev/null && _PYTHON=pypy

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:../../goal/" $_PYTHON package.py \
    --license_base "${srcdir}"/licenses --override_pypy_c \
    ../../goal/pypy3-c \
    ../../../ pypy3 pypy3-c \
    "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt/pypy3/lib
  cp ../../goal/libpypy3-c.so "${pkgdir}"/opt/pypy3/lib/
  mkdir -p "${pkgdir}"/usr/lib
  ln -s /opt/pypy3/lib/libpypy3-c.so "${pkgdir}"/usr/lib/libpypy3-c.so

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy3-c "${pkgdir}"/usr/bin/pypy3

  install -Dm644 "${pkgdir}"/opt/pypy3/LICENSE \
    "${pkgdir}"/usr/share/licenses/pypy3/LICENSE
}
