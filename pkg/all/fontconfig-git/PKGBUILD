# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=fontconfig
pkgname=${_pkgname}-git
pkgver=2.10.92.7.g03216cc
_realver=2.10.92
pkgrel=1
epoch=1
pkgdesc="A library for configuring and customizing font access"
arch=(i686 x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
makedepends=('gperf')
options=('!libtool')
conflicts=("${_pkgname}")
provides=("${_pkgname}=${_realver}")
install=fontconfig.install
source=(29-replace-bitmap-fonts.conf)
sha256sums=('76eebd24e80a4f30eb2fe595d2df349bf44e781b711a8e5a10fc6c99e10c7421')

_gitname="fontconfig"

_gitroot="git://anongit.freedesktop.org/fontconfig"
_gitref="master"

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  ./autogen.sh
  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  # not upstream, not in FC and not in Gentoo, this should probably go upstream
  # meanwhile to satisfy pages like http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html we bring it back
  install -dm755 "$pkgdir"/etc/fonts/{conf.d,conf.avail}
  install -m644 "$srcdir/29-replace-bitmap-fonts.conf" \
    "$pkgdir/etc/fonts/conf.avail"
  ln -s /etc/fonts/conf.avail/29-replace-bitmap-fonts.conf \
    "$pkgdir/etc/fonts/conf.d"

  #Install license
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
