# Contributor: David Pernot <dpernot@gmail.com>
# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=emacs-company
pkgname=${pkgbase}-git
pkgver=0.5.133.g152bc89
pkgrel=1
epoch=1
pkgdesc='Company mode'
arch=('any')
url='http://company-mode.github.com/'
license=('GPLv3+')
depends=('emacs')
provides=("${pkgbase}")
optdepends=('python-pysmell' 'pymacs')
makedepends=('python-pysmell' 'pymacs' 'git')
install="${pkgbase}.install"

_gitname="company-mode"

_gitroot="git://github.com/company-mode/company-mode.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  emacs -batch -L . -f batch-byte-compile company{,-*}.el || true
}

package() {
  cd "$srcdir/$_gitname"

  install -Ddm755 "${pkgdir}/usr/share/emacs/site-lisp/"
  install -Dm644 -t "${pkgdir}/usr/share/emacs/site-lisp/" \
    company{,-*}.el{,c}
}
