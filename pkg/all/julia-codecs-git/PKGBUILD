pkgname=julia-codecs-git
pkgver=0.1.5
pkgrel=1
pkgdesc="Codecs.jl"
url="https://github.com/dcjones/Codecs.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-codecs')
source=(git://github.com/dcjones/Codecs.jl)
md5sums=('SKIP')

pkgver() {
  cd Codecs.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Codecs.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Codecs"

  cp -a src "${pkgdir}/${site_dir}/Codecs"
}
