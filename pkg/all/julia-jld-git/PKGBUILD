pkgname=julia-jld-git
pkgver=0.5.9.0.4.gf2d1b32
pkgrel=1
pkgdesc="JLD.jl"
url="https://github.com/JuliaLang/JLD.jl"
arch=('any')
license=('MIT')
# TODO version depends
makedepends=('git')
depends=('julia' 'julia-compat' 'julia-hdf5' 'julia-fileio')
provides=('julia-jld')
source=(git://github.com/JuliaLang/JLD.jl)
md5sums=('SKIP')

pkgver() {
  cd JLD.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd JLD.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/JLD/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/JLD/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
