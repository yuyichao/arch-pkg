pkgname=julia-glpk-git
pkgver=0.2.16.0.3.g88df83c
pkgrel=1
pkgdesc="GLPK.jl"
url="https://github.com/JuliaOpt/GLPK.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat' 'glpk')
provides=('julia-glpk')
source=(git://github.com/JuliaOpt/GLPK.jl)
md5sums=('SKIP')

pkgver() {
  cd GLPK.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd GLPK.jl

  julia -f deps/build.jl
}

package() {
  cd GLPK.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/GLPK"

  cp -a src deps "${pkgdir}/${site_dir}/GLPK"

  rm "${pkgdir}/${site_dir}/GLPK/deps/build.jl"
}
