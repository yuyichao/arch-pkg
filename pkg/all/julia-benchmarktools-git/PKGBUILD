pkgname=julia-benchmarktools-git
pkgver=0.0.1.0.4.gde9ae10
pkgrel=1
pkgdesc="BenchmarkTools.jl"
url="https://github.com/JuliaCI/BenchmarkTools.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-benchmarktools')
source=(git://github.com/JuliaCI/BenchmarkTools.jl)
md5sums=('SKIP')

pkgver() {
  cd BenchmarkTools.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd BenchmarkTools.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/BenchmarkTools/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/BenchmarkTools/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
