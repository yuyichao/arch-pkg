# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
pkgname=gdb-git
pkgver=87063.92fce9b
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('expat' 'guile' 'ncurses' 'python2' 'xz')
makedepends=('git')
provides=('gdb')
conflicts=('gdb')
backup=('etc/gdb/gdbinit')
options=('!libtool')
install=gdb.install
source=('gdb::git+git://sourceware.org/git/binutils-gdb.git')
md5sums=('SKIP')

pkgver() {
  cd gdb
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd gdb

  # fixes build, copied from the gdb PKGBUILD
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb

  ./configure --prefix=/usr --disable-nls \
              --disable-werror \
              --with-system-readline \
              --with-python=/usr/bin/python2 \
              --with-system-gdbinit=/etc/gdb/gdbinit

  make
}

package() {
  cd gdb

  # install only gdb, otherwise it would install files already provided by binutils
  make DESTDIR="$pkgdir/" install-gdb

  # install "custom" system gdbinit
  install -dm755 "$pkgdir/etc/gdb"
  touch "$pkgdir/etc/gdb/gdbinit"
}
