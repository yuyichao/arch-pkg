#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgbase=gammaray-qt5-git
pkgname=gammaray-qt5-git
true && pkgname=(gammaray-qt5-git gammaray-libs-qt5-git gammaray-common-git)
pkgver=v1.3.2.747.g911aaf7
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
license=('GPL')
makedepends=(git cmake qt5-base graphviz)
url='http://www.kdab.com/gammaray'
pkgdesc="GammaRay is a tool to poke around in a Qt-application and also to manipulate the application to some extent"
options=('debug' 'strip')

_gitname='GammaRay'

_gitroot='https://github.com/KDAB/GammaRay.git'
_gitref="master"

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
  make install DESTDIR="${PWD}/pkg"
}

package_gammaray-qt5-git() {
  depends=(gammaray-libs-qt5-git gammaray-common-git
    'qt5-base>=5.2.0rc1' 'qt5-base<5.3')
  provides=(gammaray-qt5 gammaray)
  conflicts=(gammaray-qt5 gammaray)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr"
  cp -a usr/bin "${pkgdir}/usr"
}

package_gammaray-libs-qt5-git() {
  depends=('qt5-base>=5.2.0rc1' 'qt5-base<5.3' graphviz)
  provides=(gammaray-qt5-libs)
  conflicts=(gammaray-qt5-libs)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr"
  cp -a usr/lib "${pkgdir}/usr"
}

package_gammaray-common-git() {
  provides=(gammaray-common)
  conflicts=(gammaray-common)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr"
  cp -a usr/include usr/share "${pkgdir}/usr"
}
