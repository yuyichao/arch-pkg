# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=5.2.0
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/internet/ktorrent/'
license=(GPL2)
depends=(libktorrent kcmutils knotifyconfig kross phonon-qt5)
makedepends=(extra-cmake-modules kdoctools boost plasma-workspace kdnssd taglib syndication kplotting kdewebkit geoip)
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin' 'geoip: info widget plugin'
            'syndication: syndication plugin' 'kdewebkit: search plugin, syndication plugin' 'kplotting: statistics plugin')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"
        https://invent.kde.org/network/ktorrent/-/commit/9747a8f524942b57e2fd42254c3de73f9d42918c.patch
        https://invent.kde.org/network/ktorrent/-/commit/f4c1c91d9b091941ebb4dae6aef95bcf9bc1c388.patch)
sha256sums=('d1031d745c1ace02d11b59c426610aee779d208e3d1ae81f8559c265173732ff'
            'fc20b1f44b478524b61727f928e3403f19577b8c5c9e2e10bda3429248ae2471'
            '0a894fe4b7c929451c7947d00e767560a99e3d53f5d7d0ac20e5ae8347ee69ee')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 --no-backup-if-mismatch < ../9747a8f524942b57e2fd42254c3de73f9d42918c.patch
  patch -Np1 --no-backup-if-mismatch < ../f4c1c91d9b091941ebb4dae6aef95bcf9bc1c388.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DWITH_SYSTEM_GEOIP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
