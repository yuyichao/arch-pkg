# Maintainer: Michael Whapples <mwhapples@aim.com>
# $Id: PKGBUILD 120714 2011-04-26 05:27:06Z jgc $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=at-spi2-atk
pkgname=${pkgbase}-git
_realver=2.7.2
pkgver=20130221
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk' 'dconf')
makedepends=('intltool')
install="${pkgbase}.install"
options=('!libtool')
groups=('gnome')
conflicts=("${pkgbase}")
provides=("${pkgbase}=${_realver}")

_gitname="at-spi2-atk"

_gitroot="git://git.gnome.org/at-spi2-atk"
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  # BUILD HERE

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  # autoreconf --force --install

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
