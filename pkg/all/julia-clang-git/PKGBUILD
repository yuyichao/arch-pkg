pkgname=julia-clang-git
pkgver=0.0.0.397.ca3880c
pkgrel=1
pkgdesc="Clang.jl"
url="https://github.com/ihnorton/Clang.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-datastructures' 'clang')
provides=('julia-clang')
options=('debug' 'strip')
source=(git://github.com/ihnorton/Clang.jl)
md5sums=('SKIP')

pkgver() {
  cd Clang.jl

  echo "0.0.0.$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd Clang.jl

  julia -f deps/build.jl
}

package() {
  cd Clang.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.txt "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Clang"

  cp -a src deps "${pkgdir}/${site_dir}/Clang"

  rm "${pkgdir}/${site_dir}/Clang/deps/build.jl"
}
