pkgname=julia-clang-git
pkgver=0.0.0.409.b8f67c5
pkgrel=2
pkgdesc="Clang.jl"
url="https://github.com/ihnorton/Clang.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-datastructures' 'clang')
provides=('julia-clang')
options=('debug' 'strip')
source=(git://github.com/ihnorton/Clang.jl)
md5sums=('SKIP')

pkgver() {
  cd Clang.jl

  echo "0.0.0.$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd Clang.jl

  julia -f deps/build.jl
}

package() {
  cd Clang.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/Clang/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/Clang/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done

  rm -rf "${dest_dir}/deps/"build.jl
}
