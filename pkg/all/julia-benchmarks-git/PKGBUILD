pkgname=julia-benchmarks-git
pkgver=0.0.0.51.49f90b3
pkgrel=1
pkgdesc="Benchmarks.jl"
url="https://github.com/johnmyleswhite/Benchmarks.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-benchmarks')
source=(git://github.com/johnmyleswhite/Benchmarks.jl)
md5sums=('SKIP')

pkgver() {
  cd Benchmarks.jl

  echo "0.0.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  cd Benchmarks.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/Benchmarks/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/Benchmarks/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
