pkgname=julia-pycall-git
pkgver=1.91.4.0.4.g1d0d17d
pkgrel=1
pkgdesc="PyCall.jl"
url="https://github.com/stevengj/PyCall.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia julia-conda julia-macrotools julia-versionparsing python)
provides=('julia-pycall')
source=(git://github.com/stevengj/PyCall.jl)
md5sums=('SKIP')

pkgver() {
  cd PyCall.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd PyCall.jl/deps

  JULIA_DEPOT_PATH="${srcdir}/.julia" julia --startup-file=no build.jl
}

package() {
  cd PyCall.jl

  . /usr/lib/julia/julia-install-pkg.sh PyCall "${pkgdir}" "${pkgname}"
}
