pkgname=julia-pycall-git
pkgver=1.4.0.0.29.g97ba3f0
pkgrel=1
pkgdesc="PyCall.jl"
url="https://github.com/stevengj/PyCall.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-conda')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-macrotools' 'python')
provides=('julia-pycall')
source=(git://github.com/stevengj/PyCall.jl)
md5sums=('SKIP')

pkgver() {
  cd PyCall.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd PyCall.jl/deps

  julia -f build.jl
}

package() {
  cd PyCall.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/PyCall/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/PyCall/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done

  rm -rf "${dest_dir}/deps/"build.jl
}
