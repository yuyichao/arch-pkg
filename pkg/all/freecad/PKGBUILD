# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad
pkgver=0.13.1830
pkgrel=6
pkgdesc='A general purpose 3D CAD modeler'
arch=('i686' 'x86_64')
url='http://www.freecadweb.org/'
license=('GPL')
depends=('boost-libs' 'curl' 'opencascade>=6.6.0' 'pivy-hg' 'python2-pyqt4'
  'xerces-c' 'libspnav' 'shared-mime-info' 'hicolor-icon-theme'
  'python2-matplotlib')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'xerces-c'
  'desktop-file-utils' 'cmake' 'coin>=3.1.3-9')
optdepends=('python2-matplotlib' 'pycollada-git')
options=(!libtool !staticlibs debug strip)
install=freecad.install
source=("http://downloads.sourceforge.net/sourceforge/free-cad/freecad-${pkgver}.tar.gz"
  "freecad-0.13.diff"
  "${pkgname}.desktop"
  "${pkgname}.xml"
  "BRepOffsetAPI_MakePipeShellPyImp.patch")
md5sums=('13b8fddef12f5f8419dcf4adbab596eb'
         '493490db7844d7d5b9426fab8da5acea'
         '5775026f19fa208e4b7c007b0d1a9ef7'
         'c2f4154c8e4678825411de8e7fa54c6b'
         '8aeceda60d5260d8c201201ac06e5335')

prepare() {
  cd "${srcdir}/freecad-${pkgver}/"

  # compatibility issues with OCC-6.6
  find -type f \( -iname '*.h' -o  -iname '*.hxx' \
    -o  -iname '*.cpp' -o  -iname '*.cxx' \) \
    -exec grep -q 'BRepTools::OuterShell' {} \; \
    -exec sed -e "s|BRepTools|BRepClass3d|g" -i {} \;

  # these patch contain some code taken from upstream
  # thanks to cbuehler
  patch -Np1 -i ../freecad-0.13.diff
  patch -p0 -i ../BRepOffsetAPI_MakePipeShellPyImp.patch
}

build() {
  cd "${srcdir}/freecad-${pkgver}/"

  mkdir -p build
  cd build

  export CXXFLAGS="${CXXFLAGS} -DBOOST_SIGNALS_NO_DEPRECATION_WARNING"

  cmake -DCMAKE_INSTALL_PREFIX=/usr/lib/freecad .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DOCC_INCLUDE_DIR=/opt/opencascade/inc/ \
    -DCMAKE_INSTALL_DATADIR=/usr/share/freecad \
    -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
    -DCMAKE_INSTALL_DOCDIR=/usr/share/freecad/doc \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DFREECAD_USE_EXTERNAL_PIVY=ON
  make

  # Builds Qt plugin
  cd ../src/Tools/plugins/widget/
  qmake-qt4 plugin.pro
  make
}

package() {
  cd "${srcdir}/freecad-${pkgver}/build"

  # Install main program
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
  mkdir -p "${pkgdir}/usr/bin/"
  ln -sf "../lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -sf "../lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"

  # Installs Qt plugin
  install -Dm755 ../src/Tools/plugins/widget/libFreeCAD_widgets.so \
    "${pkgdir}/usr/lib/qt4/plugins/designer/libFreeCAD_widgets.so"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"

  # Mime info
  install -D -m644 "${srcdir}/${pkgname}.xml" \
    "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
}
