pkgname=julia-statsbase-git
pkgver=0.6.14
pkgrel=1
pkgdesc="StatsBase.jl"
url="https://github.com/JuliaStats/StatsBase.jl"
arch=('any')
license=('LGPLv3')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-arrayviews' 'julia-compat')
provides=('julia-statsbase')
source=(git://github.com/JuliaStats/StatsBase.jl)
md5sums=('SKIP')

pkgver() {
  cd StatsBase.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd StatsBase.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/StatsBase"

  cp -a src "${pkgdir}/${site_dir}/StatsBase"
}
