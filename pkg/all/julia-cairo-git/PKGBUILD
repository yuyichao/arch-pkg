pkgname=julia-cairo-git
pkgver=0.2.27
pkgrel=1
pkgdesc="Cairo.jl"
url="https://github.com/JuliaLang/Cairo.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-color' 'julia-bindeps' 'julia-graphics'
         'glib2' 'pango' 'cairo')
provides=('julia-cairo')
source=(git://github.com/JuliaLang/Cairo.jl)
md5sums=('SKIP')

pkgver() {
  cd Cairo.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Cairo.jl

  julia -f deps/build.jl
}

package() {
  cd Cairo.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Cairo"

  cp -a deps src "${pkgdir}/${site_dir}/Cairo"

  rm "${pkgdir}/${site_dir}/Cairo/deps/build.jl"
}
