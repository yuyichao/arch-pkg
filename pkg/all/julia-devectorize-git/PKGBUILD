pkgname=julia-devectorize-git
pkgver=0.4.1.0.2.g92f2002
pkgrel=1
pkgdesc="Devectorize.jl"
url="https://github.com/lindahua/Devectorize.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-devectorize')
source=(git://github.com/lindahua/Devectorize.jl)
md5sums=('SKIP')

pkgver() {
  cd Devectorize.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Devectorize.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Devectorize"

  cp -a src "${pkgdir}/${site_dir}/Devectorize"
}
