# Maintainer: Alper KANAT <alperkanat@raptiye.org>

pkgbase=python-django
pkgname="${pkgbase}-git"
_realver=1.5
pkgver=20130312
pkgrel=1
pkgdesc="A high-level Python Web framework - development python3 version."
url="http://www.djangoproject.com"
arch=('any')
license=('BSD')
depends=('python>=3.3' 'python<3.4')
makedepends=('git' 'python-distribute')
provides=("${pkgbase}=${_realver}" 'django-python3-git' 'django-git-py3')
conflicts=("${pkgbase}" 'django-python3-git' 'django-git-py3')
replaces=('django-python3-git' 'django-git-py3')
optdepends=('mysql-python: for MySQL backend'
  'python-psycopg2: for PostgreSQL backend'
  'python-pysqlite: for SQlite backend')

_gitroot="git://github.com/django/django.git"

_gitname="django"
_gitbranch="master"

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  python setup.py build
}

package() {
  cd "$srcdir/$_gitname"

  # first copying django
  python setup.py install --root="$pkgdir" --optimize=1

  # copying license information
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # copying readme information
  install -D -m644 README.rst "$pkgdir/usr/share/doc/$pkgname/README"
  install -D -m644 INSTALL "$pkgdir/usr/share/doc/$pkgname/INSTALL"
  install -D -m644 AUTHORS "$pkgdir/usr/share/doc/$pkgname/AUTHORS"

  # copying man files
  cd "$srcdir/$_gitname/docs/man"
  install -D -m644 daily_cleanup.1 \
    "$pkgdir/usr/share/man/$pkgname/daily_cleanup.1"
  install -D -m644 django-admin.1 \
    "$pkgdir/usr/share/man/$pkgname/django-admin.1"
  install -D -m644 gather_profile_stats.1 \
    "$pkgdir/usr/share/man/$pkgname/gather_profile_stats.1"

  # copying bash completion script
  cd "$srcdir/$_gitname/extras"
  install -D -m644 django_bash_completion \
    "$pkgdir/etc/bash_completion.d/django-admin3.py"
  sed -i "$pkgdir/etc/bash_completion.d/django-admin3.py" \
    -e 's/django-admin/django-admin3/g'

  mv "$pkgdir/usr/bin/django-admin.py" "$pkgdir/usr/bin/django-admin3.py"
}
