pkgname=julia-contour-git
pkgver=0.0.7.0.17.gbdabbcd
pkgrel=1
pkgdesc="Contour.jl"
url="https://github.com/tlycken/Contour.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-fixedsizearrays')
provides=('julia-contour')
source=(git://github.com/tlycken/Contour.jl)
md5sums=('SKIP')

pkgver() {
  cd Contour.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Contour.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Contour"

  cp -a src "${pkgdir}/${site_dir}/Contour"
}
