# Maintainer: Yichao Yu <yyc1992 at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=hotot
pkgname=('hotot-data-git' 'hotot-gtk2-git' 'hotot-gtk3-git'
  'hotot-qt4-git' 'hotot-kde-git' 'hotot-qt5-git')
pkgver=0.9.8.14.2.ged2ff01
pkgrel=1
epoch=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
makedepends=('git' 'cmake' 'intltool' 'python2' 'qtwebkit' 'qt5-webkit')
makedepends+=('kdebase-runtime' 'automoc4')
install="hotot-git.install"

_gitname="hotot"

_gitroot="https://github.com/lyricat/Hotot.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref"
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref" --
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe --tags | sed -e 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  msg "Creating make environment..."
  mkdir -p build
  cd build
  [[ -f CMakeCache.txt ]] && rm -v CMakeCache.txt
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DWITH_GTK2=On -DWITH_GTK3=On -DWITH_CHROME=Off \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 -DWITH_KDE_QT=On \
    -DWITH_QT5=On
  make
  make install DESTDIR="${PWD}/pkg"
}

package_hotot-data-git() {
  depends=('hicolor-icon-theme')
  provides=("${pkgname/-git}")
  conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname/build/pkg/usr/share"

  install -d "$pkgdir/usr/share"
  for dir in *; do
    # desktop files
    [ "$dir" = applications ] && continue
    # kde icon
    [ "$dir" = apps ] && continue
    cp -a "$dir" "$pkgdir/usr/share"
  done
}

package_hotot-qt4-git() {
  arch=('i686' 'x86_64')
  depends=('hotot-data-git' 'qtwebkit')
  provides=("${pkgname/-git}" "hotot-qt-git" "hotot-qt")
  conflicts=("${pkgname/-git}" "hotot-qt")
  replaces=("hotot-qt-git")
  options=('strip' 'debug')
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-qt "$pkgdir/usr/bin/hotot-qt"
  install -Dm644 usr/share/applications/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-qt.desktop"
}

package_hotot-qt5-git() {
  arch=('i686' 'x86_64')
  depends=('hotot-data-git' 'qt5-webkit')
  provides=("${pkgname/-git}")
  conflicts=("${pkgname/-git}")
  options=('strip' 'debug')
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-qt5 "$pkgdir/usr/bin/hotot-qt5"
  install -Dm644 usr/share/applications/hotot-qt5.desktop \
    "$pkgdir/usr/share/applications/hotot-qt5.desktop"
}

package_hotot-kde-git() {
  arch=('i686' 'x86_64')
  depends=('hotot-data-git' 'kdebase-runtime')
  provides=("${pkgname/-git}")
  conflicts=("${pkgname/-git}")
  options=('strip' 'debug')
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-kde "$pkgdir/usr/bin/hotot-kde"
  install -Dm644 usr/share/applications/hotot-kde.desktop \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
  cp -a usr/share/apps "$pkgdir/usr/share/"
}

package_hotot-gtk2-git() {
  depends=('hotot-data-git' 'pywebkitgtk' 'python2-notify'
    'python-keybinder' 'dbus-python' 'desktop-file-utils')
  optdepends=('libappindicator: unity menubar integration')
  provides=("${pkgname/-git}" 'hotot')
  conflicts=("${pkgname/-git}" 'hotot')
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-gtk2 "$pkgdir/usr/bin/hotot-gtk2"
  install -Dm644 usr/share/applications/hotot-gtk2.desktop \
    "$pkgdir/usr/share/applications/hotot-gtk2.desktop"
  install -d "$pkgdir/usr/lib/python2.7/site-packages"
  cp -a usr/lib/python2.7/site-packages/hotot_gtk2 \
    "$pkgdir/usr/lib/python2.7/site-packages/"
}

package_hotot-gtk3-git() {
  depends=('hotot-data-git' 'python2-gobject' 'dbus-python'
    'webkitgtk3' 'desktop-file-utils')
  optdepends=('libappindicator: unity menubar integration')
  provides=("${pkgname/-git}")
  conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-gtk3 "$pkgdir/usr/bin/hotot-gtk3"
  install -Dm644 usr/share/applications/hotot-gtk3.desktop \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
  install -d "$pkgdir/usr/lib/python2.7/site-packages"
  cp -a usr/lib/python2.7/site-packages/hotot_gtk3 \
    "$pkgdir/usr/lib/python2.7/site-packages/"
}
