pkgname=julia-siunits-git
pkgver=0.0.5.0.15.ge47a8bf
pkgrel=1
pkgdesc="SIUnits.jl"
url="https://github.com/Keno/SIUnits.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-texextensions' 'julia-compat')
provides=('julia-siunits')
source=(git://github.com/Keno/SIUnits.jl)
md5sums=('SKIP')

pkgver() {
  cd SIUnits.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd SIUnits.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/SIUnits"

  cp -a src "${pkgdir}/${site_dir}/SIUnits"
}
