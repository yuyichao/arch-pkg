pkgname=julia-mathprogbase-git
pkgver=0.4.1.0.5.gd78c6ec
pkgrel=1
pkgdesc="MathProgBase.jl"
url="https://github.com/JuliaOpt/MathProgBase.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-mathprogbase')
source=(git://github.com/JuliaOpt/MathProgBase.jl)
md5sums=('SKIP')

pkgver() {
  cd MathProgBase.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd MathProgBase.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/MathProgBase"

  cp -a src test "${pkgdir}/${site_dir}/MathProgBase"
}
