# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-shared
pkgver=3.1.5
pkgrel=1
pkgdesc="Shared components of seafile: seafile-daemon, libseafile, libseafile python bindings, manuals"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('ccnet>=1.4.2' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'fuse')
makedepends=('vala' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
install=seafile-shared.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz")
sha256sums=('385cfce6479f715e3e180865a61776769a2418554ea332b6d8ca2cd6e7b2ae0a')

build() {
  cd "$srcdir/seafile-${pkgver}"
  ./autogen.sh
  ./configure --disable-server --disable-console --prefix=/usr \
    PYTHON=/usr/bin/python2
  make -j1
}

package() {
  cd "${srcdir}/seafile-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Remove seafile binary
  rm -rf "${pkgdir}/usr/bin/seafile"
  # Remove cli client binary
  rm -rf "${pkgdir}/usr/bin/seaf-cli"

  install -D -m644 "${srcdir}"/seafile-${pkgver}/LICENCE.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENCE.txt
}
