#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgname=pyscical-git
pkgver=36.dd4bd67
pkgrel=1
epoch=1
pkgdesc="Python Scientific Calculator"
arch=('i686' 'x86_64')
provides=('pyscical')
conflicts=('pyscical')
url="https://github.com/yuyichao/pyscical"
license=('GPLv3')
depends=('python' 'python-scipy' 'bash' 'python-matplotlib' 'python-cffi')
optdepends=('ipython: for a better instracting shell')
makedepends=('git' 'cmake' 'coreutils')
options=('debug' 'strip')

_gitname='pyscical'

_gitroot='git://github.com/yuyichao/pyscical.git'
_gitref=master
exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/$_gitname/build"

  make install DESTDIR="${pkgdir}"
}
