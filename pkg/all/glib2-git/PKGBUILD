# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: kfgz <kfgz@interia.pl>

pkgbase=glib2
pkgname=$pkgbase-git
_realver=2.37.6
pkgver=2.39.4.70.g299ca6d
pkgrel=1
epoch=1
pkgdesc="Common C routines used by GTK+ and other libs"
arch=(i686 x86_64)
url=http://www.gtk.org/
license=(LGPL)
depends=(elfutils python2)
makedepends=(gtk-doc git)
provides=("$pkgbase=$_realver")
conflicts=($pkgbase)
source=(
  $pkgbase.sh
  $pkgbase.csh)
sha256sums=('9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
            '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3')
sha512sums=('dca2bc74d2013fcb24145ac794eef457aa3213c039e40a1a26ca5017694930768e7c80e334e17a56834549dff6549c781ddd91fae6c7bbb26fdd6a083ad8217a'
            'c3899eb7fa5482ce8a35fe02db90fd0f928d50aa7e4365a9529ef35a2dcd1ed86d5a24f6bc5c635ef5b2d95a0ebfebc2bb6bc90404c99f6fb7484ed2fa032c06')
options=(!libtool !docs !emptydirs !staticlibs debug strip)

_gitname=glib

_gitroot=http://git.gnome.org/browse/glib
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname/"

  msg "Building..."
  export CFLAGS="${CFLAGS} -Wno-return-type"
  ./autogen.sh --prefix=/usr \
    --libdir=/usr/lib \
    --sysconfdir=/etc \
    --with-pcre=system \
    --disable-fam \
    --enable-debug=yes \
    --disable-compile-warnings
  find -name Makefile -exec sed -i 's:PYTHON = /usr/bin/python$:&2:g' '{}' \;
  make
}

package() {
  cd "$srcdir/$_gitname/"
  make DESTDIR="$pkgdir" install

  cd ../
  for i in $pkgbase.{,c}sh; do
    install -Dm755 $i "$pkgdir/etc/profile.d/$i"
  done

  find "$pkgdir/usr/share/bash-completion/completions/" -type f \
    -exec chmod -x '{}' \;
  sed -i 's:^#!/usr/bin/env python$:&2:g' "$pkgdir/usr/bin/gdbus-codegen"
}
