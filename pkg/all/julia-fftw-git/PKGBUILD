pkgname=julia-fftw-git
pkgver=1.2.0.0.2.g60e5169
pkgrel=1
pkgdesc="FFTW.jl"
url="https://github.com/JuliaMath/FFTW.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia julia-abstractffts julia-fftw_jll julia-linearalgebra julia-reexport)
provides=(julia-fftw)
source=(git://github.com/JuliaMath/FFTW.jl)
md5sums=('SKIP')

pkgver() {
  cd FFTW.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd FFTW.jl/deps

  JULIA_DEPOT_PATH="${srcdir}/.julia" julia --startup-file=no build.jl
}

package() {
  cd FFTW.jl

  . /usr/lib/julia/julia-install-pkg.sh FFTW "${pkgdir}" "${pkgname}"
}
