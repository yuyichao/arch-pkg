# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: Artyom Olshevskiy <siasia@siasia>

pkgname=python2-djblets-git
pkgver=1224.13b473e
pkgrel=1
pkgdesc="Useful utility functions and classes for use with Django"
url="http://github.com/djblets/djblets"
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('git' 'python2-setuptools' 'gettext'
  'python2-django-pipeline' 'python2-django' 'lessc' 'nodejs-uglify-js')
conflicts=('djblets' 'python2-djblets')
provides=('djblets=0.8' 'python2-djblets=0.8')
source=('git+git://github.com/djblets/djblets' 'django-17.patch')
md5sums=('SKIP'
         '31dfada28ddbd3200f08bd46f32d16d3')

pkgver() {
  cd djblets
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd djblets

  patch -p1 < ../django-17.patch
}

package() {
  cd djblets

  python2 setup.py install --root="$pkgdir/" --optimize=1
}
