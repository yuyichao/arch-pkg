# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
pkgver=4.0.2
pkgrel=2
pkgdesc='Multiple-precision floating-point library'
arch=(x86_64)
url='https://www.mpfr.org/'
license=(LGPL)
depends=('gmp>=5.0')
source=(https://www.mpfr.org/mpfr-$pkgver/mpfr-${pkgver}.tar.xz
        target-h.patch)
sha256sums=('1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
            '43d705097866f59ae3b7bcd5e8e6937fec5a939a1590b027be82e5b1e3756713')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 --no-backup-if-mismatch < ../target-h.patch

  cd src
  sed -i 's/^static[^][{};=]*$/JL_MPFR_TARGET &/' *.[ch]
  local types=(void 'void *\*'
               int unsigned long 'unsigned  *long' 'intmax_t' 'uintmax_t' size_t ssize_t
               float double 'long  *double' __float128 _Decimal64
               mpfr_exp_t mpz_strptr mpfr_prec_t mp_limb_t
               'char *\*' 'const  *char *\*')
  for typ in "${types[@]}"; do
    sed -i "s/^${typ}\$/JL_MPFR_TARGET &/" *.[ch]
    sed -i 's/^${typ}\b *mpfr_[^][{};=]*\$/JL_MPFR_TARGET &/' *.[ch]
  done
}

build() {
  cd $pkgname-$pkgver

  CFLAGS+=' -DJL_MPFR_TARGET=__attribute__\(\(target_clones\(\"default,ssse3,avx2,avx512f\"\)\)\)'
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd $pkgname-$pkgver
  make check
  make check-exported-symbols
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
