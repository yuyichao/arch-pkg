# Maintainer: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgbase=python-sphinx_rtd_theme
pkgname=('python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
pkgdesc='Python Sphinx Read The Docs Theme'
pkgver=0.1.5
pkgrel=1
arch=('any')
url='https://github.com/snide/sphinx_rtd_theme'
license=('MIT')
source=("https://pypi.python.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-$pkgver.tar.gz")

md5sums=('341550cdf06e2b5ed1546c3b311c3a7c')

build() {
  cd sphinx_rtd_theme-$pkgver
  # remove build directory (avoid sed issues)
  [[ -e build ]] && rm -rf build
  # python builds
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2
}

package_python-sphinx_rtd_theme() {
  pkgdesc='Python3 Sphinx Read The Docs Theme'
  depends=('python')
  arch=('i686' 'x86_64')

  cd sphinx_rtd_theme-$pkgver
  python setup.py build --build-lib=build/python \
    install --root="$pkgdir" --optimize=1
}

package_python2-sphinx_rtd_theme() {
  pkgdesc='Python2 Sphinx Read The Docs Theme'
  depends=('python2')
  arch=('i686' 'x86_64')

  cd sphinx_rtd_theme-$pkgver
  python2 setup.py build --build-lib=build/python2 \
    install --root="$pkgdir" --optimize=1
}
