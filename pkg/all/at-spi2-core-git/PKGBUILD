# Maintainer: Michael Whapples <mwhapples@aim.com>
# $Id: PKGBUILD 120767 2011-04-26 14:35:02Z ibiru $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=at-spi2-core
pkgname=${pkgbase}-git
_realver=2.7.5
pkgver=20130223
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus-core' 'glib2-git' 'libxtst')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
options=('!libtool')
groups=('gnome')
conflicts=("${pkgbase}")
provides=("${pkgbase}=${_realver}")

_gitname="at-spi2-core"

_gitroot="git://git.gnome.org/at-spi2-core"
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  # BUILD HERE
  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  # autoreconf --force --install

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
