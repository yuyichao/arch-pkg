# Original author: Sven-Hendrik Haase (svenstaro) <sh@lutzhaase.com>
# Maintainer: Zack Buhman (buhman) <zack@buhman.org>
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>

USE_PYPY=1
USE_SHARED=1
USE_LTO=0

pkgname=pypy-hg
pkgver=72371.fbd0dadee790
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2' 'pypy=2.3')
options=('debug' 'strip')
makedepends=('python2' 'mercurial')
conflicts=('pypy')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')

source=('hg+https://bitbucket.org/pypy/pypy'
  'numpy-hash.patch'
  'numpy-scalar-iter.patch')
md5sums=('SKIP'
         '9597a408cf206a5b7d938f8667003bbb'
         '10651460643787bd4504418e9531fe91')

pkgver() {
  cd "${srcdir}"/pypy
  echo "$(hg id -n).$(hg id -i)"
}

build() {
  cd "${srcdir}/pypy"
  patch -Np1 < ../numpy-hash.patch
  patch -Np1 < ../numpy-scalar-iter.patch

  cd "${srcdir}/pypy/pypy/goal"

  _PYTHON=python2
  ((USE_PYPY)) && which pypy &> /dev/null && _PYTHON=pypy
  export CFLAGS
  export LDFLAGS

  CFLAGS+=" -g -O3"
  translate_options=()

  ((USE_SHARED)) && translate_options+=('--shared')

  ((USE_LTO)) && {
    CFLAGS+=' -flto -fno-fat-lto-objects -march=native'
    LDFLAGS+=' -g -march=native -flto=8 -fuse-linker-plugin -fuse-ld=gold -O3'
    translate_options+=('--gcrootfinder=shadowstack')
  }

  $_PYTHON ../../rpython/bin/rpython -Ojit "${translate_options[@]}" \
    targetpypystandalone
}

package() {
  cd "${srcdir}"
  mkdir -p licenses/{libbz2-,openssl}
  ln -sf /usr/share/licenses/bzip2/LICENSE licenses/libbz2-/copyright
  ln -sf /usr/share/licenses/openssl/LICENSE licenses/openssl/copyright

  cd "${srcdir}"/pypy/pypy/tool/release

  _PYTHON=python2
  ((USE_PYPY)) && which pypy 2> /dev/null && _PYTHON=pypy

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:../../goal/" $_PYTHON package.py  \
    --license_base "${srcdir}"/licenses \
    ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  ((USE_SHARED)) && {
    mkdir -p "${pkgdir}"/opt/pypy/lib
    cp ../../goal/libpypy-c.so "${pkgdir}"/opt/pypy/lib/
    mkdir -p "${pkgdir}"/usr/lib
    ln -s /opt/pypy/lib/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  }

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 "${pkgdir}"/opt/pypy/LICENSE \
    "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
