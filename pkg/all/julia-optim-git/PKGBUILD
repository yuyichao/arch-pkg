pkgname=julia-optim-git
pkgver=0.20.6.0.3.ge8e34db
pkgrel=1
pkgdesc="Optim.jl"
url="https://github.com/JuliaOpt/Optim.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia julia-compat julia-fillarrays julia-linesearches julia-linearalgebra
         julia-nlsolversbase julia-nanmath julia-parameters julia-positivefactorizations
         julia-printf julia-sparsearrays julia-statsbase)
provides=('julia-optim')
source=('git://github.com/JuliaOpt/Optim.jl')
md5sums=('SKIP')

pkgver() {
  cd Optim.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Optim.jl

  . /usr/lib/julia/julia-install-pkg.sh Optim "${pkgdir}" "${pkgname}"
}
