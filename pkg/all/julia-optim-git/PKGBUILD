pkgname=julia-optim-git
pkgver=0.4.5.0.4.g75f8238
pkgrel=1
pkgdesc="Optim.jl"
url="https://github.com/JuliaOpt/Optim.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-calculus' 'julia-dualnumbers' 'julia-positivefactorizations')
provides=('julia-optim')
source=(git://github.com/JuliaOpt/Optim.jl)
md5sums=('SKIP')

pkgver() {
  cd Optim.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Optim.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/Optim/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/Optim/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
