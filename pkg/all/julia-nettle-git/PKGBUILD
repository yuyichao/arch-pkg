pkgname=julia-nettle-git
pkgver=0.2.3.0.2.g091f877
pkgrel=1
pkgdesc="Nettle.jl"
url="https://github.com/staticfloat/Nettle.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat' 'nettle')
provides=('julia-nettle')
source=(git://github.com/staticfloat/Nettle.jl)
md5sums=('SKIP')

pkgver() {
  cd Nettle.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Nettle.jl

  julia -f deps/build.jl
}

package() {
  cd Nettle.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/Nettle/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/Nettle/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done

  rm -rf "${dest_dir}/deps/"build.jl
}
