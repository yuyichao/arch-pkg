# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux(at)free.fr>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
_gemname=json
pkgname=ruby-$_gemname
pkgver=1.8.1
pkgrel=2
arch=('any')
pkgdesc='JSON implementation as a Ruby extension in C'
url='http://flori.github.io/json/'
license=('GPL')
depends=('ruby>=2.1' 'ruby<2.2')
optdepends=('jruby' 'java-environment')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
sha256sums=('961bfbbfa9fda1e857e9c791e964e6664e0d43bf687b19669dfbc7cdbc5e0200')
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  # Removing files with references to $pkgdir
  find "$pkgdir" -depth -type f -name "Makefile*" -exec rm {} \;
}
