pkgname=julia-distances-git
pkgver=0.2.1.0.3.gac0a2c5
pkgrel=1
pkgdesc="Distances.jl"
url="https://github.com/JuliaStats/Distances.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-arrayviews' 'julia-compat')
provides=('julia-distances')
source=(git://github.com/JuliaStats/Distances.jl)
md5sums=('SKIP')

pkgver() {
  cd Distances.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Distances.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Distances"

  cp -a src "${pkgdir}/${site_dir}/Distances"
}
