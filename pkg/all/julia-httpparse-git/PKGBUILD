pkgname=julia-httpparser-git
pkgver=0.1.0.0.6.g21b2585
pkgrel=1
pkgdesc="HttpParser.jl"
url="https://github.com/JuliaWeb/HttpParser.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-httpcommon' 'julia-compat'
         'http-parser')
provides=('julia-httpparser')
source=(git://github.com/JuliaWeb/HttpParser.jl)
md5sums=('SKIP')

pkgver() {
  cd HttpParser.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd HttpParser.jl

  julia -f deps/build.jl
}

package() {
  cd HttpParser.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/HttpParser"

  cp -a src deps "${pkgdir}/${site_dir}/HttpParser"

  rm -rf "${pkgdir}/${site_dir}/HttpParser/deps/"{build.jl,src}
}
