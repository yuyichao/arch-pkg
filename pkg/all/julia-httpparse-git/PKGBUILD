pkgname=julia-httpparser-git
pkgver=0.1.2
pkgrel=1
pkgdesc="HttpParser.jl"
url="https://github.com/JuliaWeb/HttpParser.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps' 'julia-pkg-scripts')
# TODO version depends
depends=('julia' 'julia-httpcommon' 'http-parser')
provides=('julia-httpparser')
source=(git://github.com/JuliaWeb/HttpParser.jl)
md5sums=('SKIP')

pkgver() {
  cd HttpParser.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd HttpParser.jl

  julia --startup-file=no deps/build.jl
}

package() {
  cd HttpParser.jl

  . julia-install-pkg.sh HttpParser "${pkgdir}" "${pkgname}"
  rm -rf "${dest_dir}/deps/src"
}
