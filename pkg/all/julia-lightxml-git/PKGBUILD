pkgname=julia-lightxml-git
pkgver=0.1.12
pkgrel=1
pkgdesc="LightXML.jl"
url="https://github.com/JuliaLang/LightXML.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-bindeps' 'libxml2')
provides=('julia-lightxml')
options=('debug' 'strip' '!emptydirs')
source=(git://github.com/JuliaLang/LightXML.jl)
md5sums=('SKIP')

pkgver() {
  cd LightXML.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd LightXML.jl

  julia -f deps/build.jl
}

package() {
  cd LightXML.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/LightXML"

  cp -a src deps "${pkgdir}/${site_dir}/LightXML"

  rm "${pkgdir}/${site_dir}/LightXML/deps/build.jl"
}
