pkgname=julia-latexstrings-git
pkgver=0.1.6.0.2.g34f5b7d
pkgrel=1
pkgdesc="LaTeXStrings.jl"
url="https://github.com/stevengj/LaTeXStrings.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-latexstrings')
source=(git://github.com/stevengj/LaTeXStrings.jl)
md5sums=('SKIP')

pkgver() {
  cd LaTeXStrings.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd LaTeXStrings.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/LaTeXStrings/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/LaTeXStrings/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
