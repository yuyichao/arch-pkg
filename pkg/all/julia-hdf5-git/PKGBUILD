pkgname=julia-hdf5-git
pkgver=0.5.8.0.8.gd49f450
pkgrel=1
pkgdesc="HDF5.jl"
url="https://github.com/JuliaLang/HDF5.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-blosc' 'julia-compat' 'hdf5')
provides=('julia-hdf5')
source=(git://github.com/JuliaLang/HDF5.jl)
md5sums=('SKIP')

pkgver() {
  cd HDF5.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd HDF5.jl

  julia -f deps/build.jl
}

package() {
  cd HDF5.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.txt "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/HDF5"

  cp -a src deps "${pkgdir}/${site_dir}/HDF5"

  rm -rf "${pkgdir}/${site_dir}/HDF5/deps/"build.jl
}
