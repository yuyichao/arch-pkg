pkgbase=pypy-setuptools
pkgname=(pypy3-setuptools pypy-setuptools)
pkgver=5.3
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
source=(http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz)
md5sums=('f4e0aa872b3e64de38c5acb478c9452b')
makedepends=('pypy' 'pypy3')

prepare() {
  cd "${srcdir}"
  cp -r setuptools-${pkgver} pypy3-setuptools-${pkgver}

  cd "${srcdir}/"setuptools-${pkgver}
  sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/pypy3|" \
    setuptools/tests/test_resources.py
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy3|" \
    setuptools/command/easy_install.py

  cd "${srcdir}/"pypy3-setuptools-${pkgver}
  sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/pypy|" \
    setuptools/tests/test_resources.py
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy|" \
    setuptools/command/easy_install.py
}

build() {
  cd "${srcdir}"/pypy3-setuptools-${pkgver}
  pypy3 setup.py build

  cd "${srcdir}"/setuptools-${pkgver}
  pypy setup.py build
}

package_pypy-setuptools() {
  depends=('pypy')
  cd "${srcdir}/setuptools-${pkgver}"
  pypy setup.py install \
    --root="${pkgdir}" --optimize=1 --skip-build
  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy/bin/easy_install usr/bin/easy_install-pypy
}

package_pypy3-setuptools() {
  depends=("pypy3>=2.3" "pypy3<=2.4")
  cd "${srcdir}/pypy3-setuptools-${pkgver}"
  # pypy3 setup.py install \
  #   --root="${pkgdir}" --optimize=1 --skip-build \
  #   --install-lib=/opt/pypy3/lib/python3.2/site-packages
  pypy3 setup.py install \
    --root="${pkgdir}" --optimize=1 --skip-build
  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy3/bin/easy_install usr/bin/easy_install-pypy3
}
