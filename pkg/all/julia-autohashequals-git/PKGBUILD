pkgname=julia-autohashequals-git
pkgver=0.0.7.0.1.g6baf019
pkgrel=1
pkgdesc="AutoHashEquals.jl"
url="https://github.com/andrewcooke/AutoHashEquals.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-autohashequals')
options=('debug' 'strip')
source=(git://github.com/andrewcooke/AutoHashEquals.jl)
md5sums=('SKIP')

pkgver() {
  cd AutoHashEquals.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd AutoHashEquals.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/AutoHashEquals"

  cp -a src "${pkgdir}/${site_dir}/AutoHashEquals"
}
