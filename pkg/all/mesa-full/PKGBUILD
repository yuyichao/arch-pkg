# Maintainer: LEW21 <lew21@xtreeme.org>

pkgname=mesa-full
pkgver=20130224
_realver=9.2.0
pkgrel=1
pkgdesc="Full Mesa 3D graphics library with all its components, built from the git master branch. Built with Wayland support."
arch=(i686 x86_64)
url="http://mesa3d.org/"
license=('LGPL')
depends=('systemd' 'libdrm>=2.4.41' 'libxxf86vm' 'libxdamage'
  'expat' 'gcc-libs' 'libxcb' 'libxext' 'libxfixes' 'llvm' 'wayland')
makedepends=('git' 'glproto>=1.4.14' 'pkgconfig' 'imake' 'python2' 'libxml2'
  'libvdpau>=0.5' 'dri2proto>=2.6')
optdepends=('libtxc_dxtn: S3TC support'
  'mesa-demos: glxinfo and glxgears'
  'opengl-man-pages: for the OpenGL API man pages')
provides=("mesa-full-wayland=${pkgver}" "mesa=${_realver}" "libgl=${_realver}"
  "libglapi=${_realver}" "libgles=${_realver}" "libegl=${_realver}"
  "ati-dri=${_realver}" "intel-dri=${_realver}" "nouveau-dri=${_realver}"
  "khrplatform-devel=${_realver}" "libgbm=${_realver}")
conflicts=('mesa' 'libgl' 'libglapi' 'libgles' 'libegl' 'ati-dri' 'intel-dri'
  'nouveau-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'tdfx-dri'
  'unichrome-dri', 'khrplatform-devel' 'libgbm')
options=('!libtool')

_gitname="mesa"

_gitroot="git://anongit.freedesktop.org/git/mesa/mesa"
_gitref=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  # Classic r300, r600 & swrast are disabled - their Gallium versions are better.
  # Classic nouveau is for different hardware than Gallium nouveau,
  # so both are enabled.
  export PYTHON=/usr/bin/python2

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=i915,i965,nouveau,radeon,r200 \
    --with-gallium-drivers=i915,r300,r600,nouveau,svga,swrast \
    --with-egl-platforms=drm,x11,wayland \
    --enable-gallium-egl \
    --enable-gallium-gbm \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gles1 \
    --enable-gles2 \
    --enable-openvg \
    --enable-glx-tls \
    --enable-texture-float

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -sf libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"
}
