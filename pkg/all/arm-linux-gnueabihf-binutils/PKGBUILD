# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Contributor: toha257 <toha257@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: Tavian Barnes <tavianator@tavianator.com>

_target="arm-linux-gnueabihf"
pkgbase=${_target}-binutils
pkgname=(${_target}-binutils-ldscripts ${_target}-binutils)
pkgver=2.29.1
pkgrel=1
_commit=2870b1ba
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
arch=(i686 x86_64)
url="http://www.gnu.org/software/binutils/"
license=(GPL)
depends=('glibc>=2.24' 'zlib')
makedepends=('git')
options=('staticlibs' '!distcc' '!ccache')
source=(# git://sourceware.org/git/binutils-gdb.git#commit=${_commit}
        http://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2)
md5sums=('9af59a2ca3488823e453bb356fe0f113')

prepare() {
  cd binutils-${pkgver}

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  rm -rf ../binutils-build
  mkdir ../binutils-build
}

build() {
  cd binutils-build

  ../binutils-${pkgver}/configure --prefix=/usr \
      --program-prefix=${_target}- \
      --with-lib-path=/usr/lib/binutils/${_target} \
      --with-local-prefix=/usr/lib/${_target} \
      --with-sysroot=/usr/${_target} \
      --target=${_target} --host=${CHOST} --build=${CHOST} \
      --disable-nls \
      --enable-threads --enable-shared --with-pic \
      --enable-ld=default --enable-gold --enable-plugins \
      --enable-deterministic-archives \
      --disable-werror --disable-gdb --disable-sim

  # check the host environment and makes sure all the necessary tools are available
  make configure-host
  make tooldir=/usr

  local pkgdir="${srcdir}/pkg"
  rm -rf "$pkgdir"

  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  rm -f "$pkgdir"/usr/bin/{ar,as,ld,nm,objdump,ranlib,strip,objcopy,readelf}
  rm -f "$pkgdir"/usr/lib/libiberty.a
  rm -rf "$pkgdir"/usr/{share,man}
}

package_arm-linux-gnueabihf-binutils-ldscripts() {
  provides=(arm-linux-gnueabi-binutils-ldscripts)
  conflicts=(arm-linux-gnueabi-binutils-ldscripts)

  mkdir -p "$pkgdir/usr/lib/"
  cp -a pkg/usr/lib/ldscripts "$pkgdir/usr/lib/"
}

package_arm-linux-gnueabihf-binutils() {
  depends+=(arm-linux-gnueabi-binutils-ldscripts)

  cp -a pkg/usr "$pkgdir/"
  rm -rf "$pkgdir/usr/lib/ldscripts"
}
