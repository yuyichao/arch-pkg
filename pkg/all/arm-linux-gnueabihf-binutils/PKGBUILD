# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: Erico Nunes <nunes dot erico at gmail>

# build from head of release branch as bug fix releases are rare

_target="arm-linux-gnueabihf"
pkgbase=${_target}-binutils
pkgname=(${_target}-binutils-ldscripts ${_target}-binutils)
pkgver=2.25.1
pkgrel=3
_commit=2bd25930
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
arch=(i686 x86_64)
url="http://www.gnu.org/software/binutils/"
license=(GPL)
depends=('glibc>=2.22' 'zlib')
makedepends=('git')
options=(!libtool !buildflags !emptydirs)
source=(git://sourceware.org/git/binutils-gdb.git#commit=${_commit}
        binutils-e9c1bdad.patch)
md5sums=('SKIP'
         'eb3aceaab8ed26e06d505f82beb30f8f')

prepare() {
  cd binutils-gdb

  # https://sourceware.org/bugzilla/show_bug.cgi?id=16992
  patch -p1 -i ../binutils-e9c1bdad.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  rm -rf ../binutils-build
  mkdir ../binutils-build
}

build() {
  cd binutils-build

  ../binutils-gdb/configure \
      --prefix=/usr \
      --with-lib-path=/usr/lib/binutils/${_target} \
      --program-prefix=${_target}- \
      --enable-shared \
      --disable-multilib \
      --with-local-prefix=/usr/lib/${_target} \
      --with-sysroot=/usr/${_target} \
      --enable-threads --with-pic \
      --enable-ld=default --enable-gold --enable-plugins \
      --disable-nls \
      --enable-deterministic-archives \
      --target=${_target} \
      --host=$CHOST \
      --build=$CHOST \
      --disable-werror --disable-gdb

  # check the host environment and makes sure all the necessary tools are available
  make configure-host
  make tooldir=/usr

  local pkgdir="${srcdir}/pkg"
  rm -rf "$pkgdir"

  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  rm -f "$pkgdir"/usr/bin/{ar,as,ld,nm,objdump,ranlib,strip,objcopy}
  rm -f "$pkgdir"/usr/lib/libiberty.a
  rm -rf "$pkgdir"/usr/{share,man}
}

package_arm-linux-gnueabihf-binutils-ldscripts() {
  privides=(arm-linux-gnueabi-binutils-ldscripts)
  conflicts=(arm-linux-gnueabi-binutils-ldscripts)

  mkdir -p "$pkgdir/usr/lib/"
  cp -a pkg/usr/lib/ldscripts "$pkgdir/usr/lib/"
}

package_arm-linux-gnueabihf-binutils() {
  depends+=(arm-linux-gnueabi-binutils-ldscripts)

  cp -a pkg/usr "$pkgdir/"
  rm -rf "$pkgdir/usr/lib/ldscripts"
}
