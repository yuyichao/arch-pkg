pkgname=libnacs-git
pkgver=933.4cd93ca
pkgrel=1
pkgdesc='Library used for the NaCs control system'
arch=(x86_64 armv7h aarch64)
url='http://github.com/nacs-lab/libnacs'
license=('LGPL')
options=('!strip' 'debug')
depends=(llvm-libs)
makedepends=(cmake git llvm openlibm)
source=("git://github.com/nacs-lab/libnacs")
md5sums=('SKIP')

pkgver() {
  cd libnacs

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libnacs

  CFLAGS+=' -O3 -flto'
  CXXFLAGS+=' -O3 -flto'
  LDFLAGS+=' -O3 -flto'

  mkdir -p build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_BINDIR=/usr/bin -DCMAKE_INSTALL_DATADIR=/usr/share \
        -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
        -DENABLE_KNACS=Off \
        -DENABLE_LLVM=On
  make
}

package() {
  optdepends=(openlibm)
  cd libnacs/build

  make install DESTDIR="${pkgdir}"

  ver=$(cat "${pkgdir}/usr/lib/pkgconfig/nacs-utils.pc" | grep '^Version: ')
  ver=${ver#Version: }
  # This extra assignment needed to fool the stupid context-independent validation
  # from `makepkg`.
  pv=("libnacs=$ver")

  provides=("${pv[@]}")
}
