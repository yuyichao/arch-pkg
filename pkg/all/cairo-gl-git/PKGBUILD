# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Aljosha Papsch <papsch.al@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Disfido <disfido@gmail.com>

pkgbase=cairo
pkgname=$pkgbase-gl-git
pkgver=1.12.14.62.g519dff5
epoch=1
_realver=1.12.15
pkgrel=1
pkgdesc="Cairo vector graphics library with GL and EGL enabled"
arch=(i686 x86_64)
url=http://cairographics.org/
license=(LGPL MPL)
depends=(fontconfig libegl libgl "libpng>=1.6" "libpng<1.7"
  libxext libxrender 'pixman>=0.27.1')
makedepends=(gtk-doc git)
provides=("cairo=$_realver" "cairo-git=${pkgver}" "cairo-xcb=$_realver")
conflicts=(cairo{,-xcb}{,-git})
options=('!libtool' 'strip' 'debug')

_gitname=cairo

_gitroot=git://anongit.freedesktop.org/git/cairo
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname/"

  msg "Building..."
  # glesv2 and gl cannot be enabled at the same time.
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gl \
    --enable-egl \
    --enable-xcb \
    --disable-static
  make
}

package() {
  cd "$srcdir/$_gitname/"

  make DESTDIR="$pkgdir" install
}
