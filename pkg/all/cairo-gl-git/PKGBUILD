# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Aljosha Papsch <papsch.al@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Disfido <disfido@gmail.com>

pkgbase=cairo
pkgname=$pkgbase-gl-git
pkgver=20130226
_realver=1.12.13
pkgrel=1
pkgdesc="Cairo vector graphics library with GL and EGL enabled"
arch=(i686 x86_64)
url=http://cairographics.org/
license=(LGPL MPL)
depends=(fontconfig libegl libgl libpng libxext libxrender 'pixman>=0.27.1')
makedepends=(gtk-doc git)
provides=("cairo=$_realver" "cairo-git=${pkgver}" "cairo-xcb=$_realver")
conflicts=(cairo{,-xcb}{,-git})
options=(!libtool)

_gitname=cairo

_gitroot=git://anongit.freedesktop.org/git/cairo
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Building..."
  # glesv2 and gl cannot be enabled at the same time.
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gl \
    --enable-egl \
    --enable-xcb \
    --disable-static
  make
}

package() {
  cd "$srcdir/$_gitname/"
  make DESTDIR="$pkgdir" install
}
