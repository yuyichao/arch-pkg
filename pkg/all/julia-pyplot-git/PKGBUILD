pkgname=julia-pyplot-git
pkgver=2.1.1.0.13.g2dcaebf
pkgrel=1
pkgdesc="PyPlot.jl"
url="https://github.com/stevengj/PyPlot.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-pycall' 'julia-colors' 'julia-latexstrings'
         'julia-compat' 'python-matplotlib' 'julia-conda')
provides=('julia-pyplot')
source=(git://github.com/stevengj/PyPlot.jl)
md5sums=('SKIP')

pkgver() {
  cd PyPlot.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd PyPlot.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/PyPlot/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/PyPlot/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
