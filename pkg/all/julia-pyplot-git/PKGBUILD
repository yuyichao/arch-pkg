pkgname=julia-pyplot-git
pkgver=1.5.2
pkgrel=1
pkgdesc="PyPlot.jl"
url="https://github.com/stevengj/PyPlot.jl"
arch=('any')
license=('LGPLv3')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-pycall' 'julia-color'
         'julia-latexstrings')
provides=('julia-pyplot')
source=(git://github.com/stevengj/PyPlot.jl)
md5sums=('SKIP')

pkgver() {
  cd PyPlot.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd PyPlot.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/PyPlot"

  cp -a src "${pkgdir}/${site_dir}/PyPlot"
}
