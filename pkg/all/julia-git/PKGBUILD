# Maintainer: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# With contributions from many kind people at https://aur.archlinux.org/packages/julia-git/

BUILD_EXTRA=0

pkgbase=julia-git
if ((BUILD_EXTRA)); then
   pkgname=('julia-git' 'julia-doc-git' 'emacs-julia-mode-git')
else
   pkgname=('julia-git')
fi
pkgver=0.3.0.rc3.5114.g894a31e
pkgrel=1
pkgdesc="Julia is a high-level, high-performance, dynamic programming language."
arch=('any' 'i686' 'x86_64')
url="http://julialang.org"
license=('GPL')
makedepends=('gcc-fortran' 'arpack' 'fftw' 'git' 'gmp' 'libgit2' 'libunwind'
             'llvm' 'mpfr' 'pcre' 'suitesparse' 'python2-sphinx'
             'python2-sphinx_rtd_theme' 'python2-pip' 'texlive-langcjk'
             'texlive-latexextra' 'emacs' 'openspecfun' 'openblas')
options=('!emptydirs' 'staticlibs' 'debug' 'strip')
source=('git://github.com/JuliaLang/julia')
md5sums=('SKIP')

pkgver() {
  cd julia

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd julia

  git reset --hard HEAD
  git clean -fdx

  git submodule init
  git submodule sync
  git submodule update

  # patch -Np1 < ../.patch

  cd doc

  # They use the Python2 version for sphinx which has on Arch a different name
  sed 's/pip /pip2 /g' -i Makefile
}

julia_opts=(prefix=/usr sysconfdir=/etc
            USE_SYSTEM_LLVM=1
            USE_SYSTEM_LIBUNWIND=1
            USE_SYSTEM_PCRE=1
            USE_SYSTEM_LIBM=1
            USE_SYSTEM_OPENLIBM=0
            USE_SYSTEM_OPENSPECFUN=1
            USE_SYSTEM_DSFMT=0
            USE_SYSTEM_BLAS=1
            USE_SYSTEM_LAPACK=1
            USE_SYSTEM_FFTW=1
            USE_SYSTEM_GMP=1
            USE_SYSTEM_MPFR=1
            USE_SYSTEM_ARPACK=1
            USE_SYSTEM_SUITESPARSE=1
            USE_SYSTEM_RMATH=0
            USE_SYSTEM_LIBUV=0
            USE_SYSTEM_UTF8PROC=0
            USE_SYSTEM_LIBGIT2=1
            USE_INTEL_MKL=0
            USE_BLAS64=0
            USE_LLVM_SHLIB=0
            MARCH=${CARCH//_/-}
            LIBBLASNAME=libopenblas
            LIBBLAS=-lopenblas
            LIBLAPACKNAME=libopenblas
            LIBLAPACK=-lopenblas)

build() {
  cd julia

  export CC='gcc -flto'
  export CXX='g++ -flto'

  make "${PWD}/usr/bin/stringreplace" "${julia_opts[@]}"
  make release "${julia_opts[@]}"
  make debug "${julia_opts[@]}"

  if ! ((BUILD_EXTRA)); then
     return
  fi

  (# Building doc
    cd doc
    make helpdb.jl
    make man
    make html
    make latexpdf
    make info)

  (cd contrib
   emacs --batch --eval \
         '(progn
          (push "." load-path)
          (byte-compile-file "julia-mode.el"))')
}

package_julia-git() {
  arch=('i686' 'x86_64')
  depends=('arpack' 'fftw' 'gmp' 'libgit2' 'libunwind' 'llvm' 'mpfr' 'pcre'
           'hicolor-icon-theme' 'xdg-utils' 'suitesparse' 'openspecfun'
           'openblas')
  optdepends=('gnuplot: If using the Gaston Package from julia')
  install=julia.install
  conflicts=('julia')

  cd julia

  make install DESTDIR="$pkgdir" "${julia_opts[@]}"

  # Remove doc files
  rm -r "$pkgdir/usr/share/doc/julia"

  ver=$("$pkgdir/usr/bin/julia" \
            -f -e 'print(VERSION.major, ".", VERSION.minor, ".", VERSION.patch)')

  provides=("julia=2:$ver")
}

package_julia-doc-git() {
  arch=('any')
  pkgdesc+=" (Documents)"
  provides=('julia-doc')
  conflicts=('julia-doc')

  cd julia/doc/_build

  install -D -m644 man/julialanguage.1 \
          "$pkgdir"/usr/share/man/man1/julialanguage.1
  install -D -m644 texinfo/JuliaLanguage.info \
          "$pkgdir"/usr/share/info/julialanguage.info
  install -D -m644 latex/JuliaLanguage.pdf \
          "$pkgdir"/usr/share/doc/julia/julialanguage.pdf
  cp -dpr --no-preserve=ownership html "$pkgdir"/usr/share/doc/julia/
  install ../helpdb.jl "$pkgdir"/usr/share/doc/julia/helpdb.jl
}

package_emacs-julia-mode-git() {
  arch=('any')
  pkgdesc="Emacs major mode for the Julia programming language"
  provides=('emacs-julia-mode')
  conflicts=('emacs-julia-mode')

  cd julia/contrib

  mkdir -p "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode.el "$pkgdir/usr/share/emacs/site-lisp"
  install -m644 julia-mode.elc "$pkgdir/usr/share/emacs/site-lisp"
}
