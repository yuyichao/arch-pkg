# Maintainer: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# With contributions from many kind people at https://aur.archlinux.org/packages/julia-git/

BUILD_DOC=1

_pkgbase=julia
pkgbase=${_pkgbase}-git
makedepends=('gcc-fortran' 'arpack' 'fftw' 'git' 'gmp' 'libunwind' 'llvm'
  'mpfr' 'pcre' 'zlib')
if ((BUILD_DOC)); then
  makedepends+=('python2-sphinx' 'python2-sphinx_rtd_theme'
    'texlive-langcjk' 'texlive-latexextra')
  # 'python2-pip'
  pkgname=('julia-git' 'julia-doc-git')
else
  pkgname=('julia-git')
fi
pkgver=0.3.0.rc2.11.g0038e08
pkgrel=1
pkgdesc="Julia is a high-level, high-performance, dynamic programming language."
arch=('i686' 'x86_64')
url="http://julialang.org"
license=('GPL')
options=('!emptydirs' 'staticlibs' 'debug' 'strip')
source=('git://github.com/JuliaLang/julia.git')
md5sums=('SKIP')

pkgver() {
  cd $_pkgbase

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $_pkgbase/doc

  # They use the Python2 version for sphinx which has on Arch a different name
  sed s/sphinx-build/sphinx-build2/g -i Makefile
}

__build_doc() {
  cd "$srcdir/$_pkgbase/doc"

  make man
  make html
  make latexpdf
  make info
}

build() {
  cd "$srcdir/$_pkgbase"

  make prefix=/usr sysconfdir=/etc \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_INTEL_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=0

  ((BUILD_DOC)) && __build_doc
}

package_julia-git() {
  arch=('i686' 'x86_64')
  depends=('arpack' 'fftw' 'git' 'gmp' 'libunwind' 'llvm' 'mpfr'
    'pcre' 'zlib') # 'suitesparse'  'utf8proc' (AUR) 'intel-mkl' (AUR)
  optdepends=('gnuplot: If using the Gaston Package from julia')
  provides=('julia')
  conflicts=('julia')

  cd $_pkgbase

  make DESTDIR="$pkgdir" prefix=/usr sysconfdir=/etc \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_INTEL_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=0 \
    install

  # Remove doc files
  rm -vr "$pkgdir"/usr/share/julia/doc
}

package_julia-doc-git() {
  arch=('any')
  pkgdesc+=" (Documents)"
  provides=('julia-doc')
  conflicts=('julia-doc')

  cd $_pkgbase/doc/_build

  install -D -m644 man/julialanguage.1 \
    "$pkgdir/usr/share/man/man1/julialanguage.1"
  install -D -m644 texinfo/JuliaLanguage.info \
    "$pkgdir/usr/share/info/julialanguage.info"
  install -D -m644 latex/JuliaLanguage.pdf \
    "$pkgdir/usr/share/julia/doc/julialanguage.pdf"
  cp -dpr --no-preserve=ownership html "$pkgdir/usr/share/julia/doc/"
}
