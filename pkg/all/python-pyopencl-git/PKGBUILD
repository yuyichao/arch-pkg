# $Id: PKGBUILD 108477 2014-03-27 14:48:11Z fyan $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyopencl-git
pkgname=('python2-pyopencl-git' 'python-pyopencl-git')
pkgver=2014.1.0.28.g31a5b9b
pkgrel=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python. Python version on top of cffi"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
epoch=1
makedepends=('python2-setuptools' 'python-setuptools' 'libcl'
  'opencl-headers' 'mesa' 'python2-mako' 'python-mako'
  'python-numpy' 'python2-numpy')
source=('LICENSE.txt')
sha1sums=('2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7')
options=('debug' 'strip')

_gitname=pyopencl

_gitroot=https://github.com/yuyichao/pyopencl
# _gitroot=/home/yuyichao/projects/dev/pyopencl-cffi/
# _gitref=test
_gitref=master-test
# _gitroot=https://github.com/pyopencl/pyopencl
# _gitref=cffi
exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref" --
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  git submodule init
  git submodule sync
  git submodule update
  git fetch --tags "$_gitroot"
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  (_fetch_git)
  rm -rf "${srcdir}/$_gitname-python2"
  cd "$srcdir"

  cp -a pyopencl{,-python2}

  cd "${srcdir}/pyopencl"
  python ./configure.py --cl-enable-gl --no-use-shipped-boost \
    --boost-python-libname=boost_python3
  python setup.py build

  cd "$srcdir/pyopencl-python2"
  python2 ./configure.py --cl-enable-gl --no-use-shipped-boost \
    --boost-python-libname=boost_python
  python2 setup.py build
}

package_python-pyopencl-git() {
  depends=('libcl' 'opencl-headers' 'mesa' "python>=3.4" "python<3.5"
    'python-mako' 'python-pytools' 'python-setuptools' 'python-numpy'
    'gcc-libs')
  provides=(python-pyopencl)
  conflicts=(python-pyopencl)

  cd "${srcdir}/pyopencl"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyopencl-git() {
  depends=('libcl' 'opencl-headers' 'mesa' "python2" 'python2-numpy'
    'python2-mako' 'python2-pytools' 'python2-setuptools' 'gcc-libs')
  provides=(python2-pyopencl)
  conflicts=(python2-pyopencl)

  cd "${srcdir}/pyopencl-python2"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
