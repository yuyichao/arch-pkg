pkgname=julia-jump-git
pkgver=0.9.1.0.43.gcc001ea
pkgrel=1
pkgdesc="JuMP.jl"
url="https://github.com/JuliaOpt/JuMP.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-mathprogbase' 'julia-reversediffsparse'
         'julia-arrayviews' 'julia-compat' 'julia-calculus')
provides=('julia-jump')
source=(git://github.com/JuliaOpt/JuMP.jl)
md5sums=('SKIP')

pkgver() {
  cd JuMP.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd JuMP.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/JuMP"

  cp -a src "${pkgdir}/${site_dir}/JuMP"
}
