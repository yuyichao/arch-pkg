pkgname=julia-numericfuns-git
pkgver=0.2.3.0.5.g947c61f
pkgrel=1
pkgdesc="NumericFuns.jl"
url="https://github.com/lindahua/NumericFuns.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-numericfuns')
source=(git://github.com/lindahua/NumericFuns.jl)
md5sums=('SKIP')

pkgver() {
  cd NumericFuns.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd NumericFuns.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/NumericFuns"

  cp -a src "${pkgdir}/${site_dir}/NumericFuns"
}
