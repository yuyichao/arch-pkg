pkgname=julia-distributedarrays-git
pkgver=0.1.5.0.27.g2e55e2c
pkgrel=1
pkgdesc="DistributedArrays.jl"
url="https://github.com/JuliaParallel/DistributedArrays.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-distributedarrays')
source=(git://github.com/JuliaParallel/DistributedArrays.jl)
md5sums=('SKIP')

pkgver() {
  cd DistributedArrays.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd DistributedArrays.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/DistributedArrays"

  cp -a src "${pkgdir}/${site_dir}/DistributedArrays"
}
