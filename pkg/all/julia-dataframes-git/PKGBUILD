pkgname=julia-dataframes-git
pkgver=0.7.0.0.11.gc543cbe
pkgrel=1
pkgdesc="DataFrames.jl"
url="https://github.com/JuliaStats/DataFrames.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-dataarrays' 'julia-statsbase' 'julia-gzip'
         'julia-sortingalgorithms' 'julia-compat' 'julia-reexport'
         'julia-docile')
provides=('julia-dataframes')
source=(git://github.com/JuliaStats/DataFrames.jl)
md5sums=('SKIP')

pkgver() {
  cd DataFrames.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd DataFrames.jl

  site_dir=$(julia -f -e "print(((pre) -> filter((x->startswith(x, pre)), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")
  dest_dir="${pkgdir}/${site_dir}/DataFrames/"

  install -dm755 "${dest_dir}"
  # This should ignore the .* files automatically
  for f in *; do
    case "$f" in
      LICENSE*)
        cp -a "$f" "${dest_dir}"
        install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
        ln -sf "../../../..${site_dir}/DataFrames/$f" \
           "${pkgdir}/usr/share/licenses/$pkgname/"
        ;;
      appveyor.yml)
        true
        ;;
      *)
        cp -a "$f" "${dest_dir}"
        ;;
    esac
  done
}
