pkgname=julia-dataframes-git
pkgver=0.7.0.0.8.gf89fca5
pkgrel=1
pkgdesc="DataFrames.jl"
url="https://github.com/JuliaStats/DataFrames.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-dataarrays' 'julia-statsbase' 'julia-gzip'
         'julia-sortingalgorithms' 'julia-compat' 'julia-reexport'
         'julia-docile')
provides=('julia-dataframes')
source=(git://github.com/JuliaStats/DataFrames.jl)
md5sums=('SKIP')

pkgver() {
  cd DataFrames.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd DataFrames.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/DataFrames"

  cp -a src "${pkgdir}/${site_dir}/DataFrames"
}
