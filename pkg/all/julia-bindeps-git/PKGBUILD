pkgname=julia-bindeps-git
pkgver=0.3.21.0.8.g2e6ff59
pkgrel=1
pkgdesc="BinDeps.jl"
url="https://github.com/JuliaLang/BinDeps.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-uriparser' 'julia-sha')
provides=('julia-bindeps')
source=(git://github.com/JuliaLang/BinDeps.jl)
md5sums=('SKIP')

pkgver() {
  cd BinDeps.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd BinDeps.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/BinDeps"

  cp -a src "${pkgdir}/${site_dir}/BinDeps"
}
