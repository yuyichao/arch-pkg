pkgname=julia-polynomials-git
pkgver=0.0.4.0.4.geef6fac
pkgrel=1
pkgdesc="Polynomials.jl"
url="https://github.com/Keno/Polynomials.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-polynomials')
source=(git://github.com/Keno/Polynomials.jl)
md5sums=('SKIP')

pkgver() {
  cd Polynomials.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd Polynomials.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Polynomials"

  cp -a src "${pkgdir}/${site_dir}/Polynomials"
}
