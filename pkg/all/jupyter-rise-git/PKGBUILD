#

_pkgname=jupyter-rise
pkgname="${_pkgname}-git"
pkgver=4.0.0b1.4.g4d18741
pkgrel=1
pkgdesc='RISE: "Live" Reveal.js Jupyter/IPython Slideshow Extension'
url="https://github.com/damianavila/RISE"
arch=('any')
license=('BSD')
depends=('jupyter' 'python>=3.5' 'python<3.6')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(git://github.com/damianavila/RISE)
md5sums=('SKIP')
install=jupyter-rise.install

pkgver() {
  cd RISE

  git describe | sed -e 's/-/./g'
}

prepare() {
  rm -rf home
  mkdir -p home/.jupyter/nbconfig
}

build() {
  cd RISE
  python setup.py build
}

package() {
  cd RISE
  python setup.py install --prefix="${pkgdir}/usr"
  PYTHONPATH="${pkgdir}/usr/lib/python3.5/site-packages/" \
            jupyter nbextension install rise --py --system \
            --prefix="${pkgdir}/usr"
}
