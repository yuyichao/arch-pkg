pkgname=ijulia-git
pkgver=1.2.0.0.2.g1b0c30a
pkgrel=1
pkgdesc="IJulia.jl"
url="https://github.com/JuliaLang/IJulia.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-conda' 'julia-pkg-scripts')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-nettle' 'julia-json' 'julia-zmq'
         'jupyter')
provides=('ijulia')
source=(git://github.com/JuliaLang/IJulia.jl)
md5sums=('SKIP')

_site_dir_cmd=(julia --startup-file=no -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))")

pkgver() {
  cd IJulia.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd IJulia.jl

  rm -rf "${srcdir}/"{jupyter,julia}
  mkdir -p "${srcdir}/"{jupyter,julia}

  site_dir=$("${_site_dir_cmd[@]}" "/usr")
  tmp_pkgdir=${srcdir}/julia
  src_ijulia_dir=$(JULIA_PKGDIR="$tmp_pkgdir" \
                               julia --startup-file=no -e 'print(Pkg.dir("IJulia"))')
  mkdir -p "$(dirname "${src_ijulia_dir}")"
  ln -s "${PWD}" "${src_ijulia_dir}"

  cd deps/

  HOME="${srcdir}/jupyter" \
      JULIA_PKGDIR="$tmp_pkgdir" \
      IJULIA_DIR="${site_dir}/IJulia" \
      julia --startup-file=no build.jl
}

package() {
  cd IJulia.jl

  . julia-install-pkg.sh IJulia "${pkgdir}" "${pkgname}"

  # Install Jupyter kernel

  kernels_dir="${pkgdir}/usr/share/jupyter"
  install -dm755 "$kernels_dir"
  cp -a "$srcdir/jupyter/.local/share/jupyter/kernels" "$kernels_dir"
}
