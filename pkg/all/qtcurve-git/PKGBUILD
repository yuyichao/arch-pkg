# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=qtcurve-git
_realver=1.8.18
pkgver=1.9.0.rc1.52.g0f528efd
pkgrel=2
pkgdesc='A configurable set of widget styles for KDE and Gtk.'
arch=('i686' 'x86_64')
url="https://github.com/QtCurve/qtcurve"
license=('GPL')
groups=(qtcurve)
depends=(qt5-base qt5-svg libxcb qt5-x11extras
         libx11 gcc-libs kinit kdelibs4support karchive kconfig kconfigwidgets ki18n kio
         kguiaddons kiconthemes kxmlgui kdoctools kdesignerplugin frameworkintegration)
optdepends=(gtk2)
makedepends=(cmake git python mesa extra-cmake-modules gtk2)
provides=(
  "qtcurve-utils=${_realver}"
  "qtcurve-gtk2=${_realver}"
  "qtcurve-qt5=${_realver}"
  "qtcurve-utils-git"
  "qtcurve-gtk2-git"
  "qtcurve-qt5-git")
conflicts=("qtcurve-qt5" "qtcurve-gtk2" "qtcurve-utils")
options=('debug' 'strip')
source=("git+https://anongit.kde.org/qtcurve.git")
md5sums=('SKIP')

pkgver() {
  cd qtcurve

  git describe --tags | sed -e 's/-/./g'
}

build() {
  cd qtcurve

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_QT4=Off
  make
}

package() {
  cd qtcurve/build

  make DESTDIR="$pkgdir/" install
}
