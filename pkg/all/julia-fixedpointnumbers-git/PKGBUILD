pkgname=julia-fixedpointnumbers-git
pkgver=0.1.2
pkgrel=1
pkgdesc="FixedPointNumbers.jl"
url="https://github.com/JeffBezanson/FixedPointNumbers.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia')
provides=('julia-fixedpointnumbers')
source=(git://github.com/JeffBezanson/FixedPointNumbers.jl)
md5sums=('SKIP')

pkgver() {
  cd FixedPointNumbers.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd FixedPointNumbers.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/FixedPointNumbers"

  cp -a src "${pkgdir}/${site_dir}/FixedPointNumbers"
}
