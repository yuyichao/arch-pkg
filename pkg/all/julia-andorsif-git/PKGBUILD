pkgname=julia-andorsif-git
pkgver=0.0.2.0.2.g860eaeb
pkgrel=1
pkgdesc="AndorSIF.jl"
url="https://github.com/JuliaIO/AndorSIF.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-images' 'julia-fileio' 'julia-compat')
provides=('julia-andorsif')
source=(git://github.com/JuliaIO/AndorSIF.jl)
md5sums=('SKIP')

pkgver() {
  cd AndorSIF.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd AndorSIF.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/AndorSIF"

  cp -a src "${pkgdir}/${site_dir}/AndorSIF"
}
