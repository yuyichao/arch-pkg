#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgbase=gammaray-qt4-git
pkgname=gammaray-qt4-git
true && pkgname=(gammaray-qt4-git gammaray-libs-qt4-git gammaray-common-git)
pkgver=2.0.2.257.gad2cba1
_realver=2.1
pkgrel=1
epoch=2
arch=('i686' 'x86_64')
license=('GPL')
makedepends=(git cmake vtk qt4 graphviz)
url='http://www.kdab.com/gammaray'
pkgdesc="GammaRay is a tool to poke around in a Qt-application and also to manipulate the application to some extent"
options=('debug' 'strip')

_gitname='GammaRay'

_gitroot='https://github.com/KDAB/GammaRay.git'
_gitref="master"

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/^[^0-9]//' -e 's/-/./g'
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build

  # force Qt4 build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DGAMMARAY_ENFORCE_QT4_BUILD=ON \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
  make install DESTDIR="${PWD}/pkg"
}

package_gammaray-qt4-git() {
  depends=("gammaray-libs-qt4-git=${epoch}:${pkgver}"
    gammaray-common-git 'qt4>=4.8' 'qt4<4.9')
  provides=("gammaray-qt4=${_realver}" "gammaray=${_realver}")
  conflicts=(gammaray-qt4 gammaray)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr/lib/gammaray"
  cp -a usr/bin "${pkgdir}/usr"
  cp -a usr/lib/gammaray/libexec "${pkgdir}/usr/lib/gammaray"

  # These cmake files probably should belong to a separate package....
  cp -a usr/lib/cmake "${pkgdir}/usr/lib"
}

package_gammaray-libs-qt4-git() {
  depends=(vtk 'qt4>=4.8' 'qt4<4.9' graphviz "gammaray=${_realver}")
  provides=("gammaray-qt4-libs=${_realver}")
  conflicts=(gammaray-qt4-libs)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr"
  cp -a usr/lib "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/lib/gammaray/libexec"
  rm -rf "${pkgdir}/usr/lib/cmake"
}

package_gammaray-common-git() {
  provides=("gammaray-common=${_realver}")
  conflicts=(gammaray-common)
  cd "$srcdir/$_gitname/build/pkg"
  mkdir -p "${pkgdir}/usr"
  cp -a usr/include usr/share "${pkgdir}/usr"
}
