# $Id: PKGBUILD 89356 2013-04-28 19:41:34Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad-library-bzr
pkgver=r488
_bzrver=240
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
makedepends=('cmake' 'bzr')
source=('kicad-library::bzr+lp:~kicad-product-committers/kicad/library')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/kicad-library"
  echo "r$(bzr revno)"
}

build() {
  cd "$srcdir/kicad-library"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  cd "$srcdir/kicad-library/build/Release"

  make DESTDIR="$pkgdir" install

  # not libraries (and provided by kicad)
  # rm -rf "$pkgdir/usr/share/kicad/template"
}
