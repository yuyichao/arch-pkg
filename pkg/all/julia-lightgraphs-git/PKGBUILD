pkgname=julia-lightgraphs-git
pkgver=0.4.0.0.88.gd982bab
pkgrel=2
pkgdesc="LightGraphs.jl"
url="https://github.com/JuliaGraphs/LightGraphs.jl"
arch=('any')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-requires' 'julia-compat' 'julia-gzip' 'julia-statsbase'
         'julia-lightxml' 'julia-parsercombinator' 'julia-clustering'
         'julia-combinatorics')
provides=('julia-lightgraphs')
options=('debug' 'strip')
source=(git://github.com/JuliaGraphs/LightGraphs.jl)
md5sums=('SKIP')

pkgver() {
  cd LightGraphs.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd LightGraphs.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/LightGraphs"

  cp -a src "${pkgdir}/${site_dir}/LightGraphs"
}
