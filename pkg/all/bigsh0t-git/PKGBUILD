# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=bigsh0t-git
pkgver=2.0.7.g56e2101
pkgrel=1
pkgdesc='BigSh0t plugins for shotcut'
arch=('i686' 'x86_64')
url="https://bitbucket.org/leo_sutic/bigsh0t"
license=('GPLv2')
depends=(shotcut frei0r-plugins)
makedepends=(cmake git patch)
provides=(bigsh0t)
conflicts=(bigsh0t)
options=('debug' 'strip')
source=("git+https://bitbucket.org/leo_sutic/bigsh0t")
md5sums=('SKIP')

pkgver() {
  cd bigsh0t

  git describe --tags | sed -e 's/-/./g'
}

build() {
  cd bigsh0t

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd bigsh0t/build

  install -dm755 "$pkgdir"/usr/lib/frei0r-1/
  install -Dm755 bigsh0t_eq_mask.so \
          bigsh0t_eq_to_rect.so \
          bigsh0t_hemi_to_eq.so \
          bigsh0t_rect_to_eq.so \
          bigsh0t_stabilize_360.so \
          bigsh0t_transform_360.so \
          "$pkgdir"/usr/lib/frei0r-1/
  install -dm755 "$pkgdir"/usr/share/shotcut/qml/filters
  cp -r shotcut/* "$pkgdir"/usr/share/shotcut/qml/filters
}
