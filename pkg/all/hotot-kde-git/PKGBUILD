# Maintainer: Yichao Yu <yyc1992 at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=hotot-kde-git
pkgver=0.9.8.14.2.ged2ff01
pkgrel=1
epoch=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('i686' 'x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
makedepends=('git' 'cmake' 'intltool' 'qtwebkit' 'kdebase-runtime')
depends=('hotot-data-git' 'kdebase-runtime')
provides=("${pkgname/-git}")
conflicts=("${pkgname/-git}")
options=('strip' 'debug')

_gitname="hotot"

_gitroot="https://github.com/lyricat/Hotot.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref"
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref" --
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe --tags | sed -e 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  msg "Creating make environment..."
  mkdir -p build
  cd build
  [[ -f CMakeCache.txt ]] && rm -v CMakeCache.txt
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DWITH_GTK=Off -DWITH_CHROME=Off -DWITH_KDE=On \
    -DWITH_QT5=Off -DHOTOT_KDE_BIN_NAME=hotot-kde
  make
  make install DESTDIR="${PWD}/pkg"
}

package() {
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-kde "$pkgdir/usr/bin/hotot-kde"
  install -Dm644 usr/share/applications/hotot-kde.desktop \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
  cp -a usr/share/apps "$pkgdir/usr/share/"
}
