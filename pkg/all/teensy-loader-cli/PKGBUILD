# Creator: Peter Feigl <craven@gmx.net>
# Modified: Josh Kropf <josh@slashdev.ca>
pkgname=teensy-loader-cli
pkgver=2.0
pkgrel=5
pkgdesc="Command line loader for the teensy microprocessor boards"
arch=('i686' 'x86_64')
url="http://www.pjrc.com/teensy/loader_cli.html"
license=('GPL2')
depends=('gcc')
source=(
  "http://www.pjrc.com/teensy/${pkgname//-/_}.${pkgver}.tar.gz"
  "http://www.pjrc.com/teensy/49-teensy.rules"
)
md5sums=('29376dce1b37826c91839a53cff75dc0'
         '9f0593b4f3dab6d9a32ebc993d6aedc3')


build() {
  cd "${srcdir}/${pkgname//-/_}"
  make
}

package() {
  cd "${srcdir}/${pkgname//-/_}"
  install -dm755 "${pkgdir}/usr/bin/"
  install -D -m755 "${srcdir}/${pkgname//-/_}/teensy_loader_cli" \
    "${pkgdir}/usr/bin/teensy-loader-cli"
  install -D -m644 "${srcdir}/49-teensy.rules" \
    "${pkgdir}/etc/udev/rules.d/49-teensy.rules"
}
