# $Id: PKGBUILD 108750 2014-04-02 19:21:18Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

# IPython 2.1 requires python 3.3 but pypy3 provides 3.2.5
_BUILD_PYPY3=0
_PATCH_REVERT_XDG=1

pkgbase=pypy-ipython
pkgname=pypy-ipython
((_BUILD_PYPY3)) && {
  true && pkgname=(pypy3-ipython pypy-ipython)
} || {
  true && pkgname=(pypy-ipython)
}
pkgver=2.1.0
pkgrel=2
pkgdesc="An enhanced Interactive Python shell. pypy version"
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=("pypy>=2.3" "pypy<=2.4" 'sqlite')
makedepends=('sqlite')
# optdepends=("wxpython: needed for ipythonx and ipython-wx"
#             "python-nose: if you want to run IPython's test suite"
#             "python-pyqt4: for ipython qtconsole"
#             "python-sip: for ipython qtconsole"
#             "python-pygments: for ipython qtconsole"
#             "python-pyzmq: for ipython notebook and ipython qtconcole"
#             "python-tornado: for ipython notebook"
#             "python-jinja: for ipython notebook"
#             "haskell-pandoc: ipython notebook conversion")
#source=("http://archive.ipython.org/release/$pkgver/ipython-$pkgver.tar.gz")
source=("https://pypi.python.org/packages/source/i/ipython/ipython-$pkgver.tar.gz"
  revert-xdg.patch
  pypy3-version.patch)
md5sums=('785c7b6364c6a0dd34aa4ea970cf83b9'
         'ea30de2d0ee31d788f3784bc1b2daf0b'
         '0b72aadfce0dc7d01da6d3262b28612e')

prepare() {
  cd "$srcdir/ipython-$pkgver"
  if ((_PATCH_REVERT_XDG)); then
    patch -Np1 < ../revert-xdg.patch
  fi
  patch -Rp0 < ../pypy3-version.patch
}

build() {
  cd "$srcdir"
  if ((_BUILD_PYPY3)); then
    rm -rf pypy3-ipython-$pkgver
    cp -r ipython-$pkgver pypy3-ipython-$pkgver
  fi
}

package_pypy3-ipython() {
  pkgdesc="An enhanced Interactive Python shell. pypy3 version."
  depends=("pypy3>=2.3" "pypy3<=2.4" 'sqlite')
  optdepends=("pypy3-pyzmq: for ipython notebook and ipython qtconcole"
    "pypy3-tornado: for ipython notebook"
    "pypy3-jinja: for ipython notebook")
  cd "$srcdir/pypy3-ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  # pypy3 setup.py install --prefix=/opt/pypy --root="$pkgdir" --optimize=1 \
  #   --install-lib=/opt/pypy3/lib/python3.2/site-packages
  pypy3 setup.py install --prefix=/opt/pypy --root="$pkgdir" --optimize=1
  # find "$pkgdir/" -name '*.pyc' -delete
  # find "$pkgdir/" -type d -empty -delete
  find "$pkgdir" -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env pypy3,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/pypy3,'

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy/"{share,bin} "$pkgdir/usr/"

  install -Dm644 docs/source/about/license_and_copyright.rst \
    "$pkgdir/usr/share/licenses/pypy3-ipython/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/-pypy3.1}
  done
  ln -s ipython-pypy3.1 ipypy3.1

  find "$pkgdir/usr/bin/" -type f -regex '.*[^3]$' -delete
  cd "$pkgdir/usr/bin/"
  for f in *; do
    mv $f $f-pypy
  done
  ln -s ipython3-pypy ipypy3

  # cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # install -Dm644 ipython.desktop \
  #   "$pkgdir/usr/share/applications/ipython.desktop"
  # install -Dm644 ipython-qtconsole.desktop \
  #   "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
}

package_pypy-ipython() {
  optdepends=("pypy-pyzmq: for ipython notebook and ipython qtconcole"
    "pypy-tornado: for ipython notebook"
    "pypy-jinja: for ipython notebook")
  cd "$srcdir/ipython-$pkgver"

  pypy setup.py install --prefix=/opt/pypy --root="$pkgdir" --optimize=1
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env pypy,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/pypy,'
  # find "$pkgdir/" -name '*.pyc' -delete

  install -m755 -d "${pkgdir}/usr"
  mv "$pkgdir/opt/pypy/"{share,bin} "$pkgdir/usr/"

  install -Dm644 docs/source/about/license_and_copyright.rst \
    "$pkgdir/usr/share/licenses/pypy-ipython/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/-pypy.1}
  done
  ln -s ipython-pypy.1 ipypy2.1
  ln -s ipython-pypy.1 ipypy.1

  cd "$pkgdir/usr/bin/"
  for f in *; do
    mv $f $f-pypy
  done
  ln -s ipython-pypy ipypy
  ln -s ipython2-pypy ipypy2

  # cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # sed -i 's/ython/ython2/g' *.desktop
  # install -Dm644 ipython.desktop \
  #   "$pkgdir/usr/share/applications/ipython2.desktop"
  # install -Dm644 ipython-qtconsole.desktop \
  #   "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
}
