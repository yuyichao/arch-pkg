pkgname=julia-blosc-git
pkgver=0.1.4.0.4.g22dbbc0
pkgrel=1
pkgdesc="Blosc.jl"
url="https://github.com/stevengj/Blosc.jl"
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('git' 'julia-bindeps')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-blosc')
source=(git://github.com/stevengj/Blosc.jl)
md5sums=('SKIP')

# TODO package library seperately

pkgver() {
  cd Blosc.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Blosc.jl/deps

  julia -f build.jl
}

package() {
  cd Blosc.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/Blosc"

  cp -a src deps "${pkgdir}/${site_dir}/Blosc"

  rm -rf "${pkgdir}/${site_dir}/Blosc/deps/"{build.jl,c-blosc-*,make.blosc}
}
