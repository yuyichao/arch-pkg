# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=gcc-python-plugin
pkgver=0.12
_gccver=4.8.0
pkgrel=1
pkgdesc="Allows the invoking of arbitrary Python scripts from inside the compiler"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gcc-python-plugin/"
license=('GPL3')
depends=("gcc=${_gccver}" 'python-six' 'python>=3.3' 'python<3.4')
makedepends=('python2')
optdepends=('python2-six: for the Python-2 plugin')
source=("https://fedorahosted.org/releases/g/c/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('0ecff018b65fea00ffa9075260031c44')

_plugindir=/usr/lib/gcc/${CHOST}/${_gccver}/plugin

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make clean
  # build python3 plugin
  make PLUGIN_PYTHONPATH=${_plugindir}/${pkgname} plugin
  mv python.so python3.so
  rm -f *.o

  # build python2 plugin
  make PYTHON=python2 PYTHON_CONFIG=python2-config \
    PLUGIN_PYTHONPATH=${_plugindir}/${pkgname} plugin
  mv python.so python2.so

  # adjust gcc-with-python script to use global plugin
  sed -i 's#$(pwd)/python.so#python#' gcc-with-python

  # not working...
  # make -C docs man
}

# TODO - run test-suite

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm755 "$pkgdir/${_plugindir}/$pkgname" "$pkgdir/usr/bin"

  make DESTDIR="$pkgdir" -C gcc-c-api install

  install -m755 python{2,3}.so "$pkgdir/${_plugindir}"
  ln -s python3.so "$pkgdir/${_plugindir}/python.so"

  find libcpychecker gccutils -type d \
    -exec install -dm755 $pkgdir/${_plugindir}/$pkgname/{} \;
  find libcpychecker gccutils -type f \
    -exec install -m644 {} $pkgdir/${_plugindir}/$pkgname/{} \;

  install -m755 gcc-with-python $pkgdir/usr/bin/
  # install -dm755 $pkgdir/usr/share/man/man1
  # install -m644 docs/_build/man/gcc-python-plugin.1 $pkgdir/usr/share/man/man1/
}
