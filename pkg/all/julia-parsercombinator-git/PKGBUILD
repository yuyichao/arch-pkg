pkgname=julia-parsercombinator-git
pkgver=1.7.6.0.1.g865cf92
pkgrel=1
pkgdesc="ParserCombinator.jl"
url="https://github.com/andrewcooke/ParserCombinator.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat' 'julia-autohashequals')
provides=('julia-parsercombinator')
options=('debug' 'strip')
source=(git://github.com/andrewcooke/ParserCombinator.jl)
md5sums=('SKIP')

pkgver() {
  cd ParserCombinator.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd ParserCombinator.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/ParserCombinator"

  cp -a src "${pkgdir}/${site_dir}/ParserCombinator"
}
