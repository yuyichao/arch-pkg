# $Id: PKGBUILD 109144 2014-04-10 20:30:56Z kkeen $
# Maintainer: Kyle Keen <keenerd at gmail dot com>
pkgbase=pypy-pyzmq
pkgname=(pypy3-pyzmq pypy-pyzmq)
pkgver=14.3.1
pkgrel=1
pkgdesc="Python bindings for zeromq, pypy version"
arch=(i686 x86_64)
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
_pypy2_deps=("pypy>=2.4" "pypy<2.5")
_pypy3_deps=("pypy3>=2.3" "pypy3<2.4")
depends=(zeromq "${_pypy2_deps[@]}" "${_pypy3_deps[@]}")
makedepends=(pypy pypy3 zeromq)
#source=(https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz)
source=(https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz
  compiler-pypy.json
  compiler-pypy3.json
  cc)
md5sums=('7196b4a6fbf98022f17ffa924be3d68d'
         '9222c8150542cf09e89ee0a4ebac4ce7'
         '833310ff1f1bfe9ca0a4d65424142b13'
         'ed7368322249136597140383cf59bceb')
options=('debug' 'strip' '!emptydirs')

build() {
  cd "$srcdir"
  find "$srcdir" -d -name '__pycache__' -exec rm -vr {} \;
  find "$srcdir" -name '*.so' -exec rm -v {} \;
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}
  cd "$srcdir/pyzmq-$pkgver"
  # py3 errors added in 2.2.0.1
  sed -i 's|except socket.error, e:|except socket.error as e:|' \
    zmq/eventloop/ioloop.py
  sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' \
    zmq/green/core.py
  sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy3|' \
    $(find ./ -name '*.py')
  pypy3 setup.py build

  cd "$srcdir/py2zmq-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy|' \
    $(find ./ -name '*.py')

  pypy setup.py build
}

_fix_py2_cffi() {
  cd "${srcdir}"
  mv -v "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json" \
    compiler.json.bak
  cp compiler-pypy.json \
    "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
  __PYZMQ_PKGDIR="${pkgdir}" PYTHONPATH="${pkgdir}/opt/pypy/site-packages" \
    PATH="${srcdir}:${PATH}" pypy -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  mv -v compiler.json.bak \
    "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
}

_fix_py3_cffi() {
  pypy3_site_path=/opt/pypy3/site-packages/
  cd "${srcdir}"
  mv -v "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json" compiler.json.bak
  cp compiler-pypy3.json "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json"
  __PYZMQ_PKGDIR="${pkgdir}" PYTHONPATH="${pkgdir}/${pypy3_site_path}" \
    PATH="${srcdir}:${PATH}" pypy3 -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  mv -v compiler.json.bak "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json"
}

package_pypy-pyzmq() {
  pkgdesc="PyPy bindings for zeromq"
  depends=(zeromq "${_pypy2_deps[@]}")
  cd "$srcdir/py2zmq-$pkgver"
  pypy setup.py install --skip-build --root="$pkgdir" --optimize=1
  (_fix_py2_cffi zmq)
}

package_pypy3-pyzmq() {
  pkgdesc="PyPy3 bindings for zeromq"
  depends=(zeromq "${_pypy3_deps[@]}")

  cd "$srcdir/pyzmq-$pkgver"
  # pypy3 setup.py install --skip-build --root="$pkgdir" --optimize=1 \
  #   --install-lib=/opt/pypy3/lib/python3.2/site-packages
  pypy3 setup.py install --skip-build --root="$pkgdir" --optimize=1
  (_fix_py3_cffi zmq)
}
