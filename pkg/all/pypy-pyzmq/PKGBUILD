# $Id: PKGBUILD 109144 2014-04-10 20:30:56Z kkeen $
# Maintainer: Kyle Keen <keenerd at gmail dot com>
pkgbase=pypy-pyzmq
pkgname=pypy-pyzmq
true && pkgname=(pypy3-pyzmq pypy-pyzmq)
pkgver=14.3.0
pkgrel=5
pkgdesc="Python bindings for zeromq, pypy version"
arch=(i686 x86_64)
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
depends=(zeromq "pypy>=2.3" "pypy<2.4")
makedepends=(pypy pypy3 zeromq)
#source=(https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz)
source=(https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz
  compiler-pypy.json
  compiler-pypy3.json)
md5sums=('92e49cc9be92f0c95a6f790a85309df5'
         '9222c8150542cf09e89ee0a4ebac4ce7'
         '7da00245ab30678eff4b2e58a6a56b13')

build() {
  cd "$srcdir"
  find "$srcdir" -d -name '__pycache__' -exec rm -vr {} \;
  find "$srcdir" -name '*.so' -exec rm -v {} \;
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}
  cd "$srcdir/pyzmq-$pkgver"
  # py3 errors added in 2.2.0.1
  sed -i 's|except socket.error, e:|except socket.error as e:|' \
    zmq/eventloop/ioloop.py
  sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' \
    zmq/green/core.py
  sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy3|' \
    $(find ./ -name '*.py')

  cd "$srcdir/py2zmq-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy|' \
    $(find ./ -name '*.py')
}

_fix_py2_cffi() {
  cd "${srcdir}"
  mv -v "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json" \
    compiler.json.bak
  cp compiler-pypy.json \
    "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
  PYTHONPATH="${pkgdir}/opt/pypy/site-packages" pypy -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  find "$pkgdir/" -type d -empty -delete
  mv -v compiler.json.bak \
    "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
}

_fix_py3_cffi() {
  cd "${srcdir}"
  mv -v \
    "${pkgdir}/opt/pypy3/lib/python3.2/site-packages/zmq/utils/compiler.json" \
    compiler.json.bak
  cp compiler-pypy3.json \
    "${pkgdir}/opt/pypy3/lib/python3.2/site-packages/zmq/utils/compiler.json"
  PYTHONPATH="${pkgdir}/opt/pypy3/lib/python3.2/site-packages" \
    pypy3 -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  find "$pkgdir/" -type d -empty -delete
  mv -v compiler.json.bak \
    "${pkgdir}/opt/pypy3/lib/python3.2/site-packages/zmq/utils/compiler.json"
}

package_pypy-pyzmq() {
  pkgdesc="PyPy bindings for zeromq"
  depends=(zeromq "pypy>=2.3" "pypy<2.4")
  cd "$srcdir/py2zmq-$pkgver"
  pypy setup.py install --root="$pkgdir" --optimize=1
  (_fix_py2_cffi zmq)
}

package_pypy3-pyzmq() {
  pkgdesc="PyPy3 bindings for zeromq"
  depends=(zeromq "pypy3>=2.1beta1" "pypy3<2.2")

  cd "$srcdir/pyzmq-$pkgver"
  pypy3 setup.py install --root="$pkgdir" --optimize=1 \
    --install-lib=/opt/pypy3/lib/python3.2/site-packages
  (_fix_py3_cffi zmq)
}
