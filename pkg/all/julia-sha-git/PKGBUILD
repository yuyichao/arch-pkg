pkgname=julia-sha-git
pkgver=0.1.0.0.15.g90144b2
pkgrel=1
pkgdesc="SHA.jl"
url="https://github.com/staticfloat/SHA.jl"
arch=('any')
license=('MIT')
makedepends=('git')
# TODO version depends
depends=('julia' 'julia-compat')
provides=('julia-sha')
source=(git://github.com/staticfloat/SHA.jl)
md5sums=('SKIP')

pkgver() {
  cd SHA.jl

  git describe | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  cd SHA.jl

  install -dm755 "${pkgdir}/usr/share/licenses/$pkgname/"
  install LICENSE.md "${pkgdir}/usr/share/licenses/$pkgname/"

  site_dir=$(julia -f -e "print(((pre) -> filter(((x) -> x[1:length(pre)] == pre), sort(LOAD_PATH, lt=(x, y) -> length(x) < length(y)))[1])(ARGS[1]))" "/usr")

  install -dm755 "${pkgdir}/${site_dir}/SHA"

  cp -a src "${pkgdir}/${site_dir}/SHA"
}
